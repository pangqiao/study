
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. 基本原理](#1-基本原理)
  - [1.1. 使用4种table和entry结构](#11-使用4种table和entry结构)
  - [1.2. 支持3种页面size](#12-支持3种页面size)
  - [1.3. Intel64新功能](#13-intel64新功能)
- [2. IA-32e paging模式下的CR3](#2-ia-32e-paging模式下的cr3)
  - [2.1. 普通的CR3结构](#21-普通的cr3结构)
  - [2.2. 2.2 在CR4.PCIDE=1时Intel64的CR3](#22-22-在cr4pcide1时intel64的cr3)
  - [2.3. PCID机制](#23-pcid机制)
    - [2.3.1. 当前的PCID值](#231-当前的pcid值)
    - [2.3.2. 默认的PCID值](#232-默认的pcid值)
    - [2.3.3. 更新PCID值](#233-更新pcid值)
    - [2.3.4. 开启PCID功能](#234-开启pcid功能)
    - [2.3.5. 关闭PCID功能](#235-关闭pcid功能)
- [3. IA-32e paging模式下的PML4E结构](#3-ia-32e-paging模式下的pml4e结构)
- [4. IA-32e paging模式下的PDPTE结构](#4-ia-32e-paging模式下的pdpte结构)
  - [4.1. 1G页面下的PDPTE结构](#41-1g页面下的pdpte结构)
  - [4.2. 4K和2M页面下的PDPTE结构](#42-4k和2m页面下的pdpte结构)
- [5. IA-32e paging模式下的PDE结构](#5-ia-32e-paging模式下的pde结构)
- [6. IA-32e paging模式下的PTE结构](#6-ia-32e-paging模式下的pte结构)
- [7. SMEP机制](#7-smep机制)
  - [7.1. User权限](#71-user权限)
  - [7.2. Supervisor权限](#72-supervisor权限)
  - [7.3. Data页和Code页](#73-data页和code页)
  - [7.4. SMEP的实施](#74-smep的实施)
  - [7.5. 检测SMEP是否支持](#75-检测smep是否支持)
  - [7.6. 使用和测试SMEP功能](#76-使用和测试smep功能)
  - [7.7. 重新设置page映射](#77-重新设置page映射)
  - [7.8. 开启SMEP功能](#78-开启smep功能)
  - [7.9. 复制到0x400000区域](#79-复制到0x400000区域)
  - [7.10. VMware中的运行结果](#710-vmware中的运行结果)
  - [7.11. Bochs 2.5.1中的运行结果](#711-bochs-251中的运行结果)
- [8. 使用IA-32e paging模式](#8-使用ia-32e-paging模式)
  - [8.1. 初始化paging结构](#81-初始化paging结构)
  - [8.2. 进入IA-32e模式](#82-进入ia-32e模式)
  - [8.3. 测试IA-32e paging模式](#83-测试ia-32e-paging模式)

<!-- /code_chunk_output -->

# 1. 基本原理

这是Intel的术语, 在AMD中是指long-mode paging模式. 开启paging机制在IA-32e模式里是必需的. 只有开启 paging 机制 IA-32e(long-mode) 才能被激活(`IA32_EFER.LMA=1`).

在 IA-32e paging 模式里, 必须开启的功能如下.

① `IA32_EFER.LME=1`: 开启long-mode.

② `CR0.PG=1`, `CR4.PAE=1`: 进入PAE paging模式, 并激活long-mode.

下面是在 IA-32e paging 模式下的页转换路径图.

![config](./images/36.png)

**灰色路线**是**2M页面**的转换, **深灰色**路线是**1G页面**的转换, 而**黑色路线**是**4K页面**的转换.

* **4KB** 页面**线性地址**构成: 9(PML4T索引, 512个项) + 9(PDPT索引, 512个项) + 9(PDT索引, 512个项) + 9(PT索引, 512个项) + 12(Page offset, 4KB页)
* **2MB** 页面**线性地址**构成: 9(PML4T索引, 512个项) + 9(PDPT索引, 512个项) + 9(PDT索引, 512个项) \+ 21(Page offset, 2MB页)
* **1GB** 页面**线性地址**构成: 9(PML4T索引, 512个项) + 9(PDPT索引, 512个项) + 30(Page offset, 1GB页)

## 1.1. 使用4种table和entry结构

在**IA-32e paging**模式下, 使用4个table和table entry结构.

① PML4T(Page Map Level-4 Table)及表内的 PML4E 结构, 这是 IA-32e paging 新增的**页转换表**, 每个表为 4K 字节. 内含 512 个 PML4E 结构.

② PDPT(Page Directory Pointer Table)及表内的 PDPE 结构(在前面的 PAE paging 描述中使用 PDPTE 术语, 指同一结构), 每个表 4K 字节, 内含 512 个 PDPE 结构.

③ PDT(Page Directory Table)及表内的PDE结构(或称 PDTE 结构), 每个表 4K 字节, 内含 512 个 PDE 结构.

④ PT(Page Table)及表内的 PTE 结构, 每个表 4K 字节, 内含 512 个 PTE 结构.

IA-32e paging模式是在PAE paging模式的基础上新增了一级table, virtual address可以使用48位宽. 在当前的**x64体系**里, 处理器**64位线性地址**空间**只实现了48位**, 高16位被用做Bit 47位的**符号扩展位**, 要么全是0值, 要么全是1值, 这是 **canonical address 形式**.

每个 table entry 结构都是 **8 个字节 64 位宽**, 而 virtual address 中的每个 table entry 索引值都是9位, 因此**每个 table 都是 512×8=4K 字节**.

## 1.2. 支持3种页面size

在 IA-32e paging 模式下新增了 1G 页面, 可以使用 3 种 page size.

① 4K页面: page frame 从 PTE 里提供.

② 2M页面: page frame 从 PDE 里提供.

③ 1G页面: page frame 从 PDPTE 里提供.

## 1.3. Intel64新功能

Intel64实现了一些新功能, 包括 **PCID 功能**和 **SMEP 功能**.

**SMEP 功能**也可以使用在 **PAE paging模式**里. 而**PCID功能**必须用在**IA-32e paging模式(！！！**)里. 这些功能在**AMD64处理器里是不提供**的.

我们将在后面的**11.5.1.3节里探讨PCID功能**, 在**11.5.6节里探讨SMEP功能**.

# 2. IA-32e paging模式下的CR3

在IA-32e paging模式下, **CR3是64位宽(！！！**). 在AMD64中CR3的结构比较简单, 然而在Intel64中CR3结构分两种情况.

① 在**不支持PCIDE功能**时, Intel64的实现与AMD64实现是一致的.

② 在**支持PCIDE功能**时, Intel64实现比较复杂.

我们先了解普通的CR3结构.

## 2.1. 普通的CR3结构

当 `CR4.PCIDE=0` 时, Intel64 与 AMD64 在 `IA-32e paging` 模式下的 CR3 如下图所示.

![config](./images/37.png)

CR3 可以使用 64 位宽, 它的 PML4T 物理地址同样遭遇到MAXPHYADDR取值的情形, **PML4T物理地址的形成**与**第4节所描述的情形一致**.

`CR3[51: 12]` 提供 **PML4T 的物理地址高位(最多40位**), **低 12 位全部补 0**,  **PML4T 地址 4K 边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

## 2.2. 2.2 在CR4.PCIDE=1时Intel64的CR3

Intel的PCID功能**只能用于IA-32e paging模式**下, CR3的结构如下图所示.

![config](./images/38.png)

在Intel64提供的新机制下, CR3变得复杂起来, 当**CR4.PCIDE=1(！！！**)时, CR3的**低12位提供一个PCID值**, 用来定义**当前的process context ID(！！！**). 当对**CR3进行更新**时, **Bit 63位(！！！**)提供一个值来决定**是否需要invalidates处理器的TLB和paging-structure cache**(这两个cache将在后面探讨).

① 当`CR3[63]=0`时, 对**CR3更新**将使**TLB**和**paging-structure cache**失效.

② 当`CR3[63]=1`时, 对CR3更新不会使TLB和paging-structure cache失效.

## 2.3. PCID机制

这个**PCID**是为**TLB**和**paging-structure cache**而产生的, CR3**低12位的PCID值**可以为**不同的linear address space(线性地址空间**)定义**一个编号**. 处理器会**在TLB和paging-structure cache**里为**不同PCID编号**的**linear address space维护一份cache(tlb和paging-structure两个！！！**).

由于**PCID值是12**位, 可以定义**最大值为0xFFF**, 共**4K的PCID值**. Intel并没有明确说明关于PCID的限制.

基于上述的思考, 我们可以认为**每个处理器**会维护共**4K份的TLB和paging-structure cahce**, 这将是一个很大的cache块, **若每份TLB为4K字节**, 那么将需要**16M的TLB**. 因此笔者对此有些疑虑. **可是PCID是Intel64上新的技术**, 许多处理器**并不支持**.

![config](./images/39.png)

注意, 上面这个图是笔者对Intel64手册里所理解出来的PCID与TLB及pagingstructure cache之间的对应关系. **每一个PCID号**对应一份**TLB和paging-structure cache**(或者其他形式).

### 2.3.1. 当前的PCID值

处理器在 **TLB 和 paging-structure cache** 中建立或者说生成 **TLB entry 和 Cache entry** 时, 只**基于当前的 PCID 值(！！！**). 直接点说就是, 处理器在**一个时间**上, 只维护**当前 PCID 编号(！！！**)的 **linear address space**(或说virtual address space)的**TLB和paging structure cache(！！！**).

当使用**mov CR3, reg64**指令对**CR3进行更新**, 并且使用**不同的PCID值**时, 处理器会**维护新的PCID值所对应的TLB与paging structure cache**.

那么, 当前的PCID就是在CR3里, 随着**使用新PCID值**进行CR3切换而**改变**.

### 2.3.2. 默认的PCID值

有**三种情况**使用**默认的PCID值(000H**).

① 当处理器**不支持PCID功能**时, 使用**默认的PCID值**.

② PCID**只能使用在IA-32e paging模式**里, 因此当处理器在**32位paging模式**及**PAE paging模式**下, 即**IA32\_EFER.LME=0**时, 使用默认的PCID值.

③ 在IA-32e paging模式下, 但**CR4.PCIDE=0**, 即**未开启PCID功能**时, 使用默认的PCID值.

**默认的PCID值为000H(！！！**), 因此在**上述情况下**, 处理器只维护**000H编号**的**TLB和paging-structure cache**, 实际效果等同于在legacy处理器上未实现PCID功能.

### 2.3.3. 更新PCID值

当执行mov CR3, reg64指令对**CR3进行刷新**时, **TLB和paging-structure cache的失效**依赖于**CR3[63]位(！！！**), 如下面的代码所示.

```x86asm
mov rax, PML4T_BASE | 0x1    ;PCID=01 值
mov cr3, rax                 ;更新 CR3
```

这个代码是在CR4.PCIDE=1的前提下, **使用了PCID值为1去更新CR3**, 并且CR3[63]=0, 表明需要更新TLB及paging-structure cahce, 那么这时候指令对TLB和paging-structure cache有下面几方面的情形.

① 使原来PCID为001H编号的**TLB无效**, 即**刷新TLB**.

② 使原来PCID为001H编号的**paging-structure cache无效**, 即**刷新paging-structure cache**.

③ 对global page无影响, **不会刷新global page(！！！**).

④ 对**其他PCID编号的TLB和paging-structure cache无影响(！！！**), 不会刷新其他PCID编号的TLB和paging-structure cache内容.

因此, 处理器会保留其他PCID编号的virtual address space在TLB及paging-structure cache的内容, 即virtual address的page及table entry.

那么从这个角度看, 处理器应该**需要保留0xFFF份virtual address space的page及table entry在TLB和paging-structure cache里**.

### 2.3.4. 开启PCID功能

在**使用PCID功能前**应**检查处理器是否支持**, 通过检查**CPUID.01H: ECX[17].PCID支持位**是否置位实现. 如果不支持, **对CR4.PCIDE进行置位会产生\#GP异常**.

PCID功能只能使用在**IA-32e模式**下, 如果当前处理器不是在IA-32e模式下(IA32_EFER.LMA=0), 对CR4.PCIDE进行置位会产生#GP异常.

只有在**当前PCID值为000H**的时候(即当前是**默认的PCID值**)才能对CR4.PCIDE置位**开启PCID功能**. 否则产生\#GP异常(也就是说, 之前已经设置了PCID值).

### 2.3.5. 关闭PCID功能

对**CR4.PCIDE清0**, 处理器将立即关闭PCID功能, 这时候**当前的PCID值**将立即切换到**000H默认的PCID值**.

在对**CR4.PCIDE清位**时, **所有的TLB**和**paging-structure cache都失效**, 包括**global page和所有的PCID编号的TLB和paging-structure cache(！！！**).

同时CR3使用11.5.2.1节图中的legacy格式, **Bit 3和Bit 4位被解析为PWT和PCD标志位**.

# 3. IA-32e paging模式下的PML4E结构

现在我们回过头来看看 `IA-32e paging` 模式下**新增的 PML4E 结构**, 如下图所示.

![config](./images/40.png)

从上图看 PML4E 并没有PS标志位, 因此Bit 7位是保留位, PML4E提供的PDPT物理基地址的形成与11.4.3节所描述的一致.

`PML4E[51: 12]`提供**PDPT的物理地址高位(最多40位**),  **低12位全部补0**,  **PDPT地址4K边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

**IA-32e paging 模式**下从**最高级table entry开始使用XD标志**, 因此 `PML4E[63]` 是 XD 标志位, 而 PAE paging 模式从 PDE 开始(因 PDPTE 在 PDPTE 寄存器内).

# 4. IA-32e paging模式下的PDPTE结构

在 IA-32e paging 模式下, 由于**新增了 1G 页面**, 因此在 PDPTE 结构里将控制 1G 页面的转换, 由 `PDPTE.PS` 标志位进行转换, 如下图所示.

![config](./images/41.png)

* 当 **PDPTE.PS=1** 时, 使用 1G 页面的 PDPTE 结构, 那么此时 PDPTE 将提供 1G page frame 地址;
* 当 **PDPTE.PS=0** 时, 使用非 1G 页面的 PDPTE 结构, 那么它将提供下一级 PDT 的地址.

## 4.1. 1G页面下的PDPTE结构

使用1G页面, PDPTE结构被解析如下图所示.

![config](./images/42.png)

同样, 在PDPTE里提供的1G page frame物理地址将依赖于MAXPHYADDR值, 1G page frame地址**低30位将补0**, 意味着它将在**1G边界上对齐**. 它也具有page的属性, 包括: D位, G位和PAT位.

1GB页面**没有PDT**, **没有PT**, **PDPTE直接指向页面**.

`PDPTE[51: 30]`提供**PDT的物理地址高位(最多22位**),  **页面地址低30位全部补0**,  **页面地址4K边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

## 4.2. 4K和2M页面下的PDPTE结构

当PDPTE.PS=0时, 意味着PDPTE将提供下一级PDT的基地址, 那么再根据PDE.PS来决定使用2M页面还是4K页面.

![config](./images/43.png)

PDT的地址是4K边界对齐, 低12位补0, 它的物理地址形成同11.4.3节所描述的一致.

`PDPTE[51: 12]`提供**PDT的物理地址高位(最多40位**),  **低12位全部补0**,  **PDT地址4K边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

# 5. IA-32e paging模式下的PDE结构

由于 `IA-32e paging` 模式是在 PAE paging 模式上扩展而来, 因此从PDE结构开始, `IA-32e paging` 模式和 PAE paging 是完全一致的.

第4.5节所描述的PAE paging模式下的PDE结构同样适合在 `IA-32e paging` 模式里. 为了保持叙述的完整性, 在这里也将PDE结构一一列出来.

下面是 **2M 页面的 PDE 结构**.

![config](./images/44.png)

对比11.4.4.1节图中的PAE paging模式的2M页面PDE结构, 它们是完全一致的.

2MB页面**没有PT**, **PDE直接指向页面**.

`PDE[51:21]`提供**页面的物理地址高位(最多31位**),  **页面地址低21位全部补0**,  **页面地址2MB边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

下面是 `IA-32e paging` 模式下4K页面的PDE结构.

![config](./images/45.png)

对比 11.4.4.2 图中的PAE paging模式的4K页面PDE结构, 它们是完全一致的.

`PDE[51:12]`提供**PT的物理地址高位(最多40位**),  **页表PT地址低12位全部补0**,  **页表PT地址4K边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

# 6. IA-32e paging模式下的PTE结构

在IA-32e paging模式下的PTE结构与PAE paging模式下的是完全一致的, 详见11.4.5节的描述, 为了保持叙述的完整性, 在这里也列举出来.

![config](./images/46.png)

对比11.4.5节图中的PAE paging模式的PTE结构, 它们是完全一致的.

`PTE[51: 12]`提供**页面的物理地址高位(最多40位**),  **页面地址低12位全部补0**,  **页面地址4K边界对齐**.

如果物理地址比较小, **bit 51往下减少**.

# 7. SMEP机制

**Intel64**的**SMEP(Supervisor-Mode Execution Prevention)机制**用来防止Supervisor执行User权限的page. 也就是说, 在**开启SMEP功能**后, 如果当前的CPL在0级、1级和2权限里, 处理器将**拒绝执行属于User权限(U/S=1)的代码page**.

## 7.1. User权限

在**table entry**里的U/S标志**位**用来定义table或page的访问权限. 当U/S=1时, table或page属于User权限, 也就是说, 它可以被User权限来访问(即3级权限). 当然在正常情况下, **Supervisor是有权限访问**的, 除了**开启SMEP功能下的User Code页面**.

## 7.2. Supervisor权限

在**U/S标志为0**时, table或page属于Supervisor权限. 在这种情况下, 只有CPL在0级、1级和2级权限下才能被访问.

## 7.3. Data页和Code页

在32位paging模式下(即non-PAE模式), page并不分Data或是Code页, 也就是说任何page都可以被执行. 在PAE paging和IA-32e paging模式里, 可以使用**XD标志位来指定是否被允许执行**. 当然是在**开启XD功能**的前提下.

在IA32\_EFER.NXE=1下, table entry的XD=1时, page是不可执行的, 这个page属于Data页. 当XD=0或者IA32\_EFER.NXE=0时, page是可执行的, 这个page属于Code页(也属于Data页).

这个Code页和segmentation机制下的Code段有些不同, Code段是不可写的, Code页可以指定为Writable(可写).

或许在paging机制下没有Code page的概念, 笔者在Intel手册里似乎没看到Code/Data page术语.

## 7.4. SMEP的实施

对于page的执行有下面几种情形.

① 在PAE paging和IA-32e paging模式下, 当IA32\_EFER.NXE=1, 并且XD=1时, page不可执行. 这种情况不属于SMEP的功能.

② 在XD=0并且CR4.SMEP=1时, 在Supervisor权限下(CPL=0, 1或2)不能执行User权限(U/S=1)的page.

③ 在XD=0并且CR4.SMEP=0时, Supervisor权限下可以执行User的page.

SMEP功能是**限制Supervisor对User page的执行**, 上面的②和③情形属于开启SMEP功能后的影响.

## 7.5. 检测SMEP是否支持

在开启前, 需要**使用CPUID的07H leaf检测是否支持SMEP功能**.

代码清单11-18(lib\cpuid.asm):

```x86asm
; -------------------------------------
;  dump_CPUID_leaf_07_ebx():
; -------------------------------------
dump_CPUID_leaf_07_ebx:
      jmp do_dump_CPUID_leaf_07_ebx
fsgsbase            db 'fsgsbase', 0
smep                 db 'smep', 0
repmovsb_ex        db 'enhanced_movsb/stosb', 0
invpcid_msg        db 'invpcid', 0
leaf_07_ebx_flags dd fsgsbase, 0, 0, 0, 0, 0, 0, smep, 0, repmovsb_ex,
invpcid_msg, -1
leaf_07_ebx_msg    db '<CPUID.07H: EBX>', 10, 0
do_dump_CPUID_leaf_07_ebx:
      mov esi, leaf_07_ebx_msg
      call puts
      mov eax, 07H
      mov ecx, 0                        ;  subleaf=0H
      cpuid
      mov esi, ebx
      mov edi, leaf_07_ebx_flags
      call dump_flags
      call println
      ret
```

这个dump\_CPUID\_leaf\_07\_ebx()函数用来打印07H leaf的subleaf=0H时返回的ebx寄存器的支持位, 下面是在Bochs 2.5里的运行结果.

![config](./images/47.png)

在Bochs里实验是因为VMware里并不支持SMEP功能, 在Bochs 2.5里笔者将FSGSBASE和SMEP功能都打开, 所以在Bochs里显示为支持的, 图中灰色框所示.

代码清单11-19(lib\page32.asm):

```x86asm
smep_enable:
      mov eax, 07
      mov ecx, 0                         ;  sub leaf=0
      cpuid
      bt ebx, 7                           ;  SMEP suport ?
      jnc smep_enable_done
      mov eax, cr4
      bts eax, SMEP_BIT                ;  enable SMEP
      mov cr4, eax
smep_enable_done:
      ret
```

这个smep\_enable()函数先检测是否支持SMEP功能, 而后再开启SMEP控制位.

## 7.6. 使用和测试SMEP功能

在这里, 我们同样对SMEP功能进行实验, 练习使用和测试SMEP功能.

>实验11-5: 使用和测试SMEP功能

实验的源代码在topic11\ex11-05\目录下.

## 7.7. 重新设置page映射

我们需要重新修改映射方式, 代码如下.

代码清单11-20(topic11\ex11-5\protected.asm):

```x86asm
; ;  为了实验: 下面将 PDE 改为 supervisor 权限
      mov DWORD [201000h + 0 * 8], 0000h | PS |  P
      mov DWORD [201000h + 0 * 8 + 4], 0
      mov DWORD [201000h + 1 * 8], 200000h | PS | P
      mov DWORD [201000h + 1 * 8 + 4], 0
;  下面将 0x400000 区域改为 user 权限
      mov DWORD [201000h + 2 * 8], 202000h | RW | US | P
      mov DWORD [201000h + 2 * 8 + 4], 0
; ;  3) 设置 PTE[0]
      ;  PTE[0] 对应 virtual address: 0x400000 到 0x400fff (4K页)
;  下面将 PTE 改为 user 权限, 以及清 XD 标志位
      mov DWORD [202000h + 0 * 8], 400000h | RW | US |P
      mov eax, [xd_bit]
      mov DWORD [202000h + 0 * 8 + 4], 0                         ;  清 XD 位
```

我们的修改如下.

① 将0到0x3FFFFF的区域修改为 supervisor权限, 因为protected.asm模块的代码运行在这个区域里.

② 将0x400000到0x400FFF的区域改为user权限, 因为我们需要在这个区域里做实验.

③ 将page 0x400000的XD标志位清0(意味着可以执行), 配合SMEP功能使用.

## 7.8. 开启SMEP功能

在0级权限代码里使用上面介绍的smep\_enable()函数来**开启SMEP功能**.

## 7.9. 复制到0x400000区域

接着将一段代码复制到0x400000区域里, 这个区域的page是属于user权限的.

代码清单11-21(topic11\ex11-5\protected.asm):

```x86asm
;  复制到 0x400000 区域
      mov esi, user_start
      mov edi, 0x400000
      mov ecx, user_end - user_start
      rep movsb
      jmp 0x400000
```

复制后, 跳转到0x400000地址上接着运行, 笔者在VMware和Bochs 2.5里分别测试了结果.

## 7.10. VMware中的运行结果

由于VMware并不支持SMEP功能, 所以在VMware上运行是正常的, 如下图所示.

![config](./images/48.png)

上面浅灰色框部分是在protected模块里运行的, 显示地址0x400000的page属于User权限(U/S=1), 接着调用smep\_enable()函数, 这个函数检测到不支持SMEP功能并不会对CR4.SMEP进行置位. 深灰色框部分在0x400000里运行正常. 它的XD标志为0, 即是可执行的page.

## 7.11. Bochs 2.5.1中的运行结果

从Bochs 2.5后新加入了SMEP功能, 笔者的测试是在Bochs 2.5.1版本下进行的, 可以开启SMEP功能, 下面是结果.

![config](./images/49.png)

在Bochs 2.5.1里运行, 体现出了SMEP功能的用处: 它在0x400000区域里产生了#PF异常, 发生在0x400000地址上, 它是在当前0级权限下执行产生的. 注意page 0x400000的XD标志为0, 属于User权限的page.

笔者并没有在真实机器里运行, 笔者的机器上不支持SMEP功能.

# 8. 使用IA-32e paging模式

在IA-32e paging模式下几乎与PAE paging模式是一致的, 除了新增的PML4T和附加的新功能, 要进入处理器的IA-32e模式(long-mode)就必须开启paging机制并且需要使用PAE机制.

>实验11-6: 使用和测试IA-32e paging模式

在这里作为一个练习实验来使用和测试IA-32e paging模式, 源代码在topic11\ex11-6\目录下, 在这个实验里, 控制权将来到long.asm模块里(这是一个long-mode的运行模块).

## 8.1. 初始化paging结构

同样在开启paging前应设置各级的table结构, long-mode的paging结构初始化代码实现在lib\page64.asm文件里, 代码比较长, 如下所示.

代码清单11-22(lib\page64.asm):

```x86asm
; ---------------------------------------------------------------
;  init_page(): 初始化 long mode 的页结构
;  描述:
;         在进入 long-mode 模式之前进行页表初始化
; ----------------------------------------------------------------
init_page:
;  lib32  :      virtual address 0xb000 map to physicall address 0xb000 with 4k-page
;  lib32  :      virtual address 0xe000 map to physicall address 0xeb000 with 4k-page
;  code32 :      virtual address 0x10000-0x13000 map to physicall address 0x10000-
0x13000 with 4K-page
;  video  :      virtual address 0xb8000 map to physicall address 0xb8000 with 4K-page
;  data   :      virtual address 0x200000 map to physicall address 0x200000 with 2M
page
;  code64 :      virtual address 0xfffffff800000000 to physicall address 0x600000
with 4K-page
;  apic:         virtual address 0x800000 map to physica address 0xFEE00000(Local APIC区域)
;  DS save:      virtual address 400000h map to physical address 400000h
;  user code64 : virtual address 00007FFF_00000000 map to physical address 800000h
      mov esi, 200000h
      mov edi, 15
      call clear_4k_pages
      mov esi, 201000h
      mov edi, 3
      call clear_4k_pages
      mov esi, 300000h
      mov edi, 5
      call clear_4K_pages
;  设置 PML4T, PML4T 的地址在 200000H
      mov DWORD [200000h], 201000h | RW | US | P             ;  PML4T[0]
      mov DWORD [200004h], 0
      ; ;  由 0FFFFFF8X_XXXXXXXXX 的 virutal address 均是用户不可访问
      mov DWORD [200000h + 1FFh * 8], 202000h | RW | P     ;  PML4T[0x1ff]
      mov DWORD [200000h + 1FFh * 8 + 4], 0
      mov DWORD [200000h + 0FFh * 8], 300000h | RW | US | P
      mov DWORD [200000h + 0FFh * 8 + 4], 0
;  设置 PDPT, 第 0 个 PDPT 在 201000H, 第 511 个 PDPT 在 202000H
      mov DWORD [201000h], 203000h | RW | US | P             ;  PDPT[0] for PML4T[0]
      mov DWORD [201004h], 0
      ;  为了 00000000_FFE00000 - 00000000_FFE01FFFh 而映射
      mov DWORD [201000h + 3 * 8], 210000h | RW | US | P
      mov DWORD [201000h + 3 * + 4], 0
      mov DWORD [202000h + 1E0h * 8], 204000h | RW | P
      mov DWORD [202000h + 1E0h * 8 + 4], 0
      ;  从0FFFFFFFF_CXXXXXXX 开始的地址
      mov DWORD [202000h + 1FFh * 8], 209000h | RW | P
      mov DWORD [202000h + 1FFh * 8 + 4], XD
      mov DWORD [300000h + 1FCh * 8], 301000h | RW | US | P
      mov DWORD [300000h + 1FCh * 8 + 4], 0
      mov DWORD [300000h + 1FFh * 8], 302000h | RW | US | P
      mov DWORD [300000h + 1FFh * 8 + 4], 0
;  set PDT
      ; **** PDT[0] for PDPT[0] for PML4T[0]***
      mov DWORD [203000h], 205000h | RW | US | P
      mov DWORD [203004h], 0
      ;  virtual address 200000h - 3FFFFFh 映射到 200000h - 3FFFFFh 上
      ;  不可执行, 用户不可访问
      ;  系统数据区
      mov DWORD [203000h + 1 * 8], 200000h | PS | RW | P
      mov DWORD [203000h + 1 * 8 + 4], XD
      mov DWORD [203000h + 2 * 8], 207000h | RW | P
      mov DWORD [203000h + 2 * 8 + 4], XD
      ;  virutal address 800000h - 9FFFFFh 映射到 0FEE00000h - 0FEFFFFFFh(2M页面)
      ;  不可执行, 用户不可访问, PCD=PWT=1
      ;  用于 local APIC 区域
      mov DWORD [203000h + 4 * 8], 0FEE00000h | PCD | PWT | PS | RW | P
      mov DWORD [203000h + 4 * 8 + 4], XD
      ;  PDT[0] for PDPT[0x1e0] for PML4T[0x1ff]
      mov DWORD [204000h], 206000h | RW | P
      mov DWORD [204004h], 0
      mov DWORD [204000h + 80h * 8], 208000h | RW | P
      mov DWORD [204000h + 80h * 8 + 4], 0
      mov DWORD [209000h + 1FFh * 8], 20A000h | RW | P
      mov DWORD [209000h + 1FFh * 8 + 4], XD
      ;  virutal address 00007FFF_00000000h - 00007FFF_001FFFFFh(2M页)
      ;  映射到物理地址 800000h
      ;  可执行, 64 位用户代码执行区域
      mov DWORD [301000h], 800000h | PS | RW | US | P
      mov DWORD [301004h], 0
      mov DWORD [302000h + 1FFh * 8], 303000h | RW | US | P
      mov DWORD [302000h + 1FFh * 8 + 4], XD
      ;  为了 00000000_FFE00000 - 00000000_FFE01FFFh 而映射
      mov DWORD [210000h + 1FFh * 8], 211000h | RW | US | P
      mov DWORD [210000h + 1FFh * 8 + 4], 0
;  set PT
      ;  virutal address 0 - 0FFFh 映射到物理地址 0 - 0FFFh 上(4K页)
      ;  no present！(保留未映射)
      mov DWORD [205000h + 0 * 8], 0000h | RW | US
      mov DWORD [205000h + 0 * 8 + 4], 0
      ;  virtual address 0B000 - 0BFFFh 映射到物理地址 0B000 - 0BFFFFh 上(4K页)
      ;  r/w=u/s=p=1
      mov DWORD [205000h + 0Bh * 8], 0B000h | RW | US | P
      mov DWORD [205000h + 0Bh * 8 + 4], 0
      ;  virtual address 0E000 - 0EFFFh 映射到物理地址 0E000 - 0EFFFh 上(4k页)
      mov DWORD [205000h + 0Eh * 8], 0E000h | RW | P
      mov DWORD [205000h + 0Eh * 8 + 4], 0
      ;  virtual address 10000h - 13FFFh 映射到物理地址 10000h - 13FFFh上(共4个4K页)
      ;  可执行, r/w=u/s=p=1
      ;  用于 long.asm 模块执行空间
      mov DWORD [205000h + 10h * 8], 10000h | RW | US | P
      mov DWORD [205000h + 10h * 8 + 4], 0
      mov DWORD [205000h + 11h * 8], 11000h | RW | US | P
      mov DWORD [205000h + 11h * 8 + 4], 0
      mov DWORD [205000h + 12h * 8], 12000h | RW | US | P
      mov DWORD [205000h + 12h * 8 + 4], 0
      mov DWORD [205000h + 13h * 8], 13000h | RW | US | P
      mov DWORD [205000h + 13h * 8 + 4], 0
      ;  virtual address 0B8000h - 0B9FFFh 映射到物理地址 0B8000h - 0B9FFFh 上(2个4K页)
      ;  不可执行, r/w=u/s=p=1
      ;  用于 video 区域
      mov DWORD [205000h + 0B8h * 8], 0B8000h | RW | US | P
      mov DWORD [205000h + 0B8h * 8 + 4], XD
      mov DWORD [205000h + 0B9h * 8], 0B9000h | RW | US | P
      mov DWORD [205000h + 0B9h * 8], XD
      ;  virutal address 0xfffffff800000000 - 0xfffffff800001fff (2 个 4K 页)
      ;  映射到物理地址 600000 - 601FFFh 上
      ;  不可执行, 用户不可访问
      ;  kernel 数据区
      mov DWORD [206000h], 600000h | RW | P
      mov DWORD [206004h], XD
      mov DWORD [206000h + 8], 601000h | RW | P
      mov DWORD [206000h + 8 + 4], XD
      ;  virtual address 0FFFFFFF8_10000000h - 0FFFFFFF8_10001FFFh(2个4K页)
      ;  映射到物理地址 602000h - 602FFFh 上
      ;  用户不可访问
      ;  kernel 执行区域
      mov DWORD [208000h], 602000h | RW | P
      mov DWORD [208004h], 0
      mov DWORD [208000h + 1 * 8], 60B000h | RW | P
      mov DWORD [208000h + 1 * 8 + 4], XD
      ;  virutal address 0FFFFFFFF_FFE00000h - 0FFFFFFFF_FFE03FFFh(4个4K页)
      ;  映射到物理地址 603000h - 606FFFh 上
      ;  用于 kernel stack 区
      mov DWORD [20A000h], 603000h | RW | P                          ;  ; ; 处理器 0
      mov DWORD [20A004h], 0
      mov DWORD [20A000h + 1 * 8], 604000h | RW | P                ;  处理器 1
      mov DWORD [20A000h + 1 * 8 + 4], 0
      mov DWORD [20A000h + 2 * 8], 605000h | RW | P                ;  处理器 2
      mov DWORD [20A000h + 2 * 8 + 4], 0
      mov DWORD [20A000h + 3 * 8], 606000h | RW | P                ;  处理器 3
      mov DWORD [20A000h + 3 * 8 + 4], 0
      ;  virutal address 0FFFFFFFF_FFE04000h - 0FFFFFFFF_FFE07FFFh(4个4K页)
      ;  映射到物理地址 60B000h - 60EFFFh 上
      ;  用于中断 handler stack 区
      mov DWORD [20A000h + 4 * 8], 60B000h | RW | P                ;  处理器 0
      mov DWORD [20A000h + 4 * 8 + 4], 0
      mov DWORD [20A000h + 5 * 8], 60C000h | RW | P                ;  处理器 1
      mov DWORD [20A000h + 5 * 8 + 4], 0
      mov DWORD [20A000h + 6 * 8], 60D000h | RW | P                ;  处理器 2
      mov DWORD [20A000h + 6 * 8 + 4], 0
      mov DWORD [20A000h + 7 * 8], 60E000h | RW | P                ;  处理器 3
      mov DWORD [20A000h + 7 * 8 + 4], 0
      ;  virutal address 00007FFF_FFE00000h - 00007FFF_FFE03FFFh(4个4K页)
      ;  映射到物理地址 607000h - 60AFFFh 上
      ;  用于 user stack 区
      mov DWORD [303000h], 607000h | RW | US | P                          ;  处理器 0
      mov DWORD [303000h + 4], 0
      mov DWORD [303000h + 1 * 8], 608000h | RW | US | P                ;  处理器 1
      mov DWORD [303000h + 1 * 8 + 4], 0
      mov DWORD [303000h + 2 * 8], 609000h | RW | US | P                ;  处理器 2
      mov DWORD [303000h + 2 * 8 + 4], 0
      mov DWORD [303000h + 3 * 8], 60A000h | RW | US | P                ;  处理器 3
      mov DWORD [303000h + 3 * 8 + 4], 0
      ;  virtual address 00000000_FFE00000 - 00000000_FFE01FFFh
      ;  映射到物理地址 60C000h - 60CFFFh
      ;  用于 0 级 的 compatibility mode stack
      mov DWORD [211000h], 60C000h | RW | P
      mov DWORD [211004h], 0
      mov DWORD [211000h + 1 * 8], 60D000h | RW | P
      mov DWORD [211004h + 1 * 8 + 4], 0
      ;  virutal address 400000h 映射到物理地址 400000h 上(使用 4K 页)
      ;  不可执行, 用户不可访问, 用于 DS save 区域
      mov DWORD [207000h], 400000h | RW | P
      mov DWORD [207004h], XD
      ret
```

这个init\_page()函数运行在32位环境下, 在开启long-mode之前运行. 为IA-32e paging模式设置了许多的映射区域, 其中包括了多个stack区域的映射(为每个处理器定义了自己的stack区域, 每个stack区域为4K), 下面是关于测试实验11-6所使用到的一些设置说明.

① 0000h到0FFFh这个地址最低端的4K页面是未映射的.

② 200000h到3FFFFFh区域被作为系统数据区, 存放着paging机制的页表结构. 使用2M页面映射, 用户权限不可访问, 并且不可执行.

③ 800000h到8FFFFFh区域使用2M面页映射, 其中一部分被作为local APIC设备的映射区域, 并且设置了PCD与PWT, 属于不可cache内存, 并且不可执行.

④ 0FFFFFFF800000000h到0FFFFFFF800000FFF区域被作为long-mode下的系统数据区域高端内存区域, long-mode的系统结构将被复制到这个区域. 用户不可访问, 并且不可执行.

少数区域使用2M页, 大部分都使用4K页映射, 在进入IA-32e模式之前, 会调用init\_page()函数进行初始化页的转换表环境.

## 8.2. 进入IA-32e模式

下面代码将开启IA-32e paging模式, 并使处理器进入IA-32e模式.

代码清单11-23(topic11\ex11-6\long.asm):

```x86asm
;  设置 CR3
      mov eax, PML4T_BASE
      mov cr3, eax
;  设置 CR4
      mov eax, cr4
      bts eax, 5                                ;  CR4.PAE=1
      mov cr4, eax
;  设置 EFER
      mov ecx, IA32_EFER
      rdmsr
      bts eax, 8                                ;  EFER.LME=1
      wrmsr
;  激活 long mode
      mov eax, cr0
      bts eax, 31
      mov cr0, eax                             ;  EFER.LMA=1
```

在激活long mode之前(IA32\_EFER.LMA=0), 处理器还是保护模式, 使IA-32e模式得到激活的条件是IA32\_EFER.LME=1并且CR0.PG=1, 即开启paging机制, 此时IA32\_EFER.LMA=1, 在开启CR0.PG之前, 必须:

① CR4.PAE=1, 开启PAE模式.

② CR3指向有效的整个页转换表结构的基地址, 即PML4T的地址.

值得注意的是, 在进入long.asm模块的protected.asm模块里已经开启了paging机制, 在对CR3设置的时候需要特别小心, 防止不正确的页映射和设置(特别是, 在已经开启PAE模式的前提下, 参见前面的11.4.1.2节所描述的PDPTE寄存器加载注意事项). 当然可以选择在进入IA-32e模式里前, 先关闭原来的保护模式的paging机制, 这是一种比较省心的做法, 在笔者的所有long-mode测试里选择关闭paging机制, 再重新设置long-mode的paging环境.

## 8.3. 测试IA-32e paging模式

最后我们在3级用户代码测试一个IA-32e paging模式, 和实验11-2、11-3一样, 我们通过打印各级table entry结构来展示IA-32e paging模式.

在lib\page64.asm文件里的dump\_long\_page()函数读取了200000h区域里的页表结构来打印页表信息, 在代码清单11-22里的init\_page()函数设置了200000h区域是用户权限不可访问的, 因此, 如果需要在用户权限代码里调用dump\_long\_page()函数, 须使用另一种途径, 在实验11-6里使用了中断调用方式来执行dump\_long\_page()函数.

代码清单11-24(topic11\ex11-6\long.asm):

```x86asm
; *
; ** 下面设置用户自定义系统服务例程 ***
; ** 使用中断调用方式调用 dump_long_page()
; *
      mov rsi, SYSTEM_SERVICE_USER0                  ;  用户自定义例程号
      mov rdi, dump_long_page                         ;  用户自定义系统服务例程
      call set_user_system_service                    ;  设置用户自定义服务例程
```

上面这段代码将设置int 40h所调用的自定义服务例程, 这个服务例程就是dump\_long\_page()函数, 这样就可以在3级权限下通过中断调用来使用dump\_long\_page()函数.

set\_user\_system\_service()函数实现在lib\lib64.asm文件里, 它的作用是注册挂接一个用户自定义例程在system\_service\_table(系统服务例程表)里. int 40h中断调用就是从这个表里读取服务例程进行调用.

代码清单11-25(topic11\ex11-6\long.asm):

```x86asm
;  ① 下面打印 virtual address 0xfffffff800000000 各级 table entry 信息
      mov esi, address_msg1
      LIB32_PUTS_CALL
      mov rsi, 0xfffffff800000000
      mov rax, SYSTEM_SERVICE_USER0       ;  传递用户自定义服务例程号
      int 40h        ;  通过 int 40h 来调用dump_long_page()函数
      LIB32_PRINTLN_CALL
;  ② 下面打印 virtual address 0x200000 各级 table entry 信息
      mov esi, address_msg2
      LIB32_PUTS_CALL
      mov rsi, 0x200000
      mov rax, SYSTEM_SERVICE_USER0
      int 40h
      LIB32_PRINTLN_CALL
;  ③ 下面打印 virtual address 0x800000 各级 table entry 信息
      mov esi, address_msg3
      LIB32_PUTS_CALL
      mov rsi, 0x800000
      mov rax, SYSTEM_SERVICE_USER0
      int 40h
      LIB32_PRINTLN_CALL
;  ④ 下面打印 virtual address 0 各级 table entry 信息
      mov esi, address_msg4
      LIB32_PUTS_CALL
      mov rsi, 0
      mov rax, SYSTEM_SERVICE_USER0
      int 40h
```

通过int 40h来调用dump\_long\_page()函数分别打印地址0xfffffff800000000、0x200000和0x800000, 以及0地址的table entry信息, 这个函数实现在lib\page64.asm文件里.

在init\_page()函数里地址0是没有被映射的, 所以这个地址是无效的, 下面是在Bochs里的运行结果(在真实机器和VMware中结果是一样的).

![config](./images/50.png)

0xfffffff800000000被以4K页来映射, 因此它具有4级的table entry结构, 而0x200000和0x800000区域是以2M页来映射, 因此它具有3级的table entry结构. 最后, 由于地址0在PTE表里没被映射, 它在PTE里出现了问题(P=0).

打印出来的前面3个虚拟地址都具有XD=1(不可执行), User权限不可访问(U/S=0), Writable(可写)的页.