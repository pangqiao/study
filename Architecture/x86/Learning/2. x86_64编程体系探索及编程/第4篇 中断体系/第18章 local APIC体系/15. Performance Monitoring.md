
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [LVT preformance monitor寄存器](#lvt-preformance-monitor寄存器)
- [PMI自动屏蔽](#pmi自动屏蔽)

<!-- /code_chunk_output -->

我们在第15章已经对Performance monitoring(性能监控)有深入的了解, 并且通过15.3.9节了解了性能监控的例子. 这个例子的中断handler是通过local APIC的LVT performance monitor寄存器来进行设置的. 

# LVT preformance monitor寄存器

在性能监控里由于被监控的事件的counter(计数器)溢出而产生PMI(preformance monitor interrupt), 从而需要调用PMI handler进行相应的处理. 

那么local APIC里的LVT performance monitor寄存器需要设置来提供对PMI handler相应的支持, 如下所示. 

![config](./images/77.png)

当bit 16位的mask被置位时, PMI是被屏蔽的. PMI支持下面几种delivery模式. 

① Fixed模式(000B): 需要显式提供PMI的中断服务例程vector. 

② SMI模式(010B): LVT performance monitor寄存器的vector域必须为0值. 

③ NMI模式(100B): PMI使用NMI的vector值(即2), performance monitor寄存器的vector域必须为0. 

因此, INIT与ExtINT delivery模式不支持使用在performance monitor寄存器上. 

>实验18-15: 测试logical processor 1的PMI

这个测试实验18\-15和实验15\-3的实现原理和方法是一样的. 有所不同的是, 在这里测试logical processor 1的PMI, 而不是BSP的PMI. 

代码清单18-37(topic18\ex18-15\protected.asm): 

```x86asm
;   设置 logical processor 1 的 IPI handler
       mov esi, PROCESSOR1_IPI_VECTOR  ;  发送给处理器 1 的 IPI vector
       mov edi, processor1_ipi_handler  ;  IPI handler
       call set_interrupt_handler
       mov esi, msg0       ;  打印信息
       call puts
;  发送 IPI 消息到 processor 1
;  使用 Fixed delivery, logical目标方式发送 IPI 给处理器1
       mov DWORD [APIC_BASE + ICR1], 02000000h   ;  处理器1(logical目标)
       mov DWORD [APIC_BASE + ICR0], LOGICAL_ID | PROCESSOR1_IPI_VECTOR
```

为了让logical processor 1执行PMI, 这里由BSP向processor 1发送一条IPI消息, 让processor 1执行自己的IPI handler. 

代码清单18-38(topic18\ex18-15\protected.asm): 

```x86asm
; ---------------------------------------
;  logical processor 1 的 IPI handler
;  desctiptor: 
;   使用 Fixed delivery 模式
; ---------------------------------------
processor1_ipi_handler: 
;  设置 LVT preformance monitor 寄存器
       mov DWORD [APIC_BASE + LVT_PERFMON], FIXED_DELIVERY | APIC_PERFMON_VECTOR
;  设置 IA32_PERFEVTSEL0 寄存器
       mov ecx, IA32_PERFEVTSEL0
       mov eax, INST_COUNT_EVENT
       mov edx, 0
       wrmsr
;  设置 counter 计数值
       mov esi, IA32_PMC0
       call write_counter_maximum
;  开启 counter
       ENABLE_IA32_PMC0
       nop
       mov DWORD [APIC_BASE + EOI], 0                ;  发送 EOI 命令
       iret
```

在processor 1执行的IPI handler里, 对processor 1的performance monitor进行设置, 这个PMI使用Fixed交付模式, 使用IA32\_PMC0进行计数, 监控事件是对执行了多少条指令进行监控. Processor 1通过这个设置后, 当counter溢出时就产生PMI. 

代码清单18-39(topic18\ex18-15\protected.asm): 

```x86asm
; -------------------------------
;  perfmon handler
; ------------------------------
apic_perfmon_handler: 
       jmp do_apic_perfmon_handler
ph_msg1 db '>>> now: enter PMI handler', 10, 0
ph_msg2 db 'exit the PMI handler <<<', 10, 0
do_apic_perfmon_handler: 
       mov esi, ph_msg1     ;  打印信息
       call puts
       call dump_apic      ;  打印 local APIC寄存器信息
;  清溢出标志位
       RESET_COUNTER_OVERFLOWr
       mov esi, ph_msg2
       call puts
;  写 EOI 命令
       mov DWORD [APIC_BASE + EOI], 0 ;  发送 EOI 命令
       iret
```

在PMI handler里打印出所有local APIC寄存器的信息, 清由IA32_PMC0计数器溢出产生的溢出状态标志位. 

下面是在笔者的Core i5处理器笔记本式计算机上的运行结果. 

![config](./images/78.png)

在上面的结果里, 我们看到: 

① logical processor 1进入了PMI handler, 它的APIC ID为0x01000000, LDR值为0x02000000(logical ID). 

② ISR的bit 51(0x33)位被置位, 指示vector为0x33的中断服务例程正在运行中. 

③ performance monitor寄存器的中断vector为0x33. 

值得注意的是, 在processor 01的IPI handler(由BSP指定处理器01执行)里发生了counter溢出, 由于IPI handler正在运行中, PMI的中断请求会被暂时抑制, 直至IPI handler运行完毕, 发送EOI命令后, PMI处理程序才能得到响应执行. 

# PMI自动屏蔽

在上面的运行结果里, 我们看到的另一个信息是, 当进入PMI handler后, local APIC将自动屏蔽PMI(performance monitor寄存器的bit 16位被置位). 

因此, 在进入PMI后, 不能响应另一个PMI请求, 除非手动清mask位, 正如在第15章里所使用的例子. 