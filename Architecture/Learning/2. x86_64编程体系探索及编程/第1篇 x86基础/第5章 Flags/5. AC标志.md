
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->



<!-- /code_chunk_output -->

eflags中的AC标志是指Alignment Check(地址中的对齐检查), 只有当同时开启CR0.AM和eflags.AC标志位时处理器才支持这项功能。

我们从下面的表格来看, 什么情况下才是有效的alignment粒度。

![config](./images/13.png)

当对上述的某一类数据类型的访问违反了它的粒度时, 会产生#AC异常。

```assembly
mov ax, WORD [0x10003]               ； 跨 WORD 类型边界
mov eax, DWORD [0x10005]             ； 跨 DWORD 类型边界
mov rax, QWORD [0x10007]             ； 跨 QWORD 类型边界
mov esi, 0x10006
lodsd                                     ； 跨 bit string类型边界
```

上面几种情况下, 都属于non-alignmnet行为。只有在权限级别3下的non-alignmnet行为才会产生#AC异常, 在0、1及2级下不会产生#AC异常。

>实验5-4: 测试Alignment Check功能

\#AC异常属于fault类型, #AC处理程序会返回到发生错误的指令继续执行, 因此在我们的#AC处理程序中必须要修正这个错误: 

代码清单5-5(topic05\ex5-4\protected.asm): 

```assembly
；-----------------------------------------------
； AC_handler(): #AC handler
；-----------------------------------------------
AC_handler: 
      jmp do_AC_handler
ac_msg1                db '---> Now, enter the #AC exception handler <---', 10
ac_msg2                db 'exception location at 0x'
ac_location        dq 0, 0
do_AC_handler: 
      pusha
      mov esi, [esp+4+4*8]
      mov edi, ac_location
      call get_dword_hex_string
      mov esi, ac_msg1
      call puts
      call println
；； 现在 disable AC 功能
      btr DWORD [esp+12+4*8], 18               ； 清elfags image中的AC标志
      popa
      add esp, 4                                    ； 忽略 error code
      iret
```

\#AC处理程序里在iret返回前, 将stack中的eflags.AC清为0, iret执行完后, eflags.AC被清0。在ring0代码里先将CR0.AM置位, 在ring3代码里, 再将eflags.AC置位。

代码清单5-6(topic05\ex5-4\protected.asm): 

```assembly
； 开启 eflags.AC 标志
      pushf
      bts DWORD [esp], 18
      mov ebx, [esp]
      popf
； test 1
      mov ax, WORD [0x10003]               ； 跨 WORD 类型边界
      push ebx
      popf
； test 2
      mov eax, DWORD [0x10005]             ； 跨 DWORD 类型边界
      push ebx
      popf
； teset 3
      mov esi, 0x10006                       ； 跨 string 类型边界
      lodsd
```

由于在#AC处理程序会将AC标志清0, 因此, 每做一次测试前, 再重新开启AC标志位。这个实验的完整源代码在topic05\ex5-4\目录下, 运行的结果如下。

![config](./images/14.png)

结果显示, 共产生了3次#AC异常, 就是我们所做的3个测试。