
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. 基本原理](#1-基本原理)
  - [1.1. 两种页表结构](#11-两种页表结构)
  - [1.2. page offset值](#12-page-offset值)
  - [1.3. Table Entry(表项)的大小](#13-table-entry表项的大小)
  - [1.4. 页表的大小](#14-页表的大小)
- [2. CR3结构](#2-cr3结构)
  - [2.1. PWT](#21-pwt)
  - [2.2. PCD](#22-pcd)
- [3. 32位paging模式下的PDE结构](#3-32位paging模式下的pde结构)
  - [3.1. 4K页面的PDE结构](#31-4k页面的pde结构)
    - [3.1.1. P(Present)标志位](#311-ppresent标志位)
    - [3.1.2. R/W(Read/Write)标志位](#312-rwreadwrite标志位)
    - [3.1.3. U/S(User/Supervisor)标志位](#313-ususersupervisor标志位)
    - [3.1.4. PWT(Page-level Write-Through)标志位](#314-pwtpage-level-write-through标志位)
    - [3.1.5. PCD(Page-level Cache Disable)标志位](#315-pcdpage-level-cache-disable标志位)
    - [3.1.6. A(Accessed)标志位](#316-aaccessed标志位)
    - [3.1.7. PS(Page Size)标志位](#317-pspage-size标志位)
  - [3.2. 4M页面的PDE结构](#32-4m页面的pde结构)
    - [3.2.1. D(Dirty)标志位](#321-ddirty标志位)
    - [3.2.2. G(Global page)标志位](#322-gglobal-page标志位)
    - [3.2.3. PAT(Page-Attribute Table)标志位](#323-patpage-attribute-table标志位)
    - [3.2.4. 保留位与忽略位](#324-保留位与忽略位)
    - [3.2.5. PS(Page Size)标志位](#325-pspage-size标志位)
  - [3.3. 4K页面的PTE结构](#33-4k页面的pte结构)
    - [3.3.1. 描述page的属性](#331-描述page的属性)
- [4. 使用32位paging](#4-使用32位paging)
  - [4.1. 配置页转换表环境](#41-配置页转换表环境)
  - [4.2. 开启PSE功能](#42-开启pse功能)
  - [4.3. 开启paging](#43-开启paging)
  - [4.4. 测试一: 在用户代码里写0x400000地址](#44-测试一在用户代码里写0x400000地址)
  - [4.5. 测试二: 在0级代码里写0x400000地址](#45-测试二在0级代码里写0x400000地址)
  - [4.6. 测试三: 在开启WP功能下, 0级代码里写0x400000地址](#46-测试三在开启wp功能下-0级代码里写0x400000地址)

<!-- /code_chunk_output -->

# 1. 基本原理

在**32位paging模式**里, **virtual address是32位宽(！！！**)。映射的**物理地址**在**支持PSE\-36机制**下是**36位或40位**(取决于**MAXPHYADDR值**), 否则**为32位**。

![config](./images/9.png)

上图显示了在**32位paging模式**下的**4K和4M页面**的page frame获取路径。**CR3提供PDT的基地址**。

**4MB页面线性地址构成: 10(PDT索引, 1024个项) \+ 22(Page offset, 4MB页**)

**灰色线路**是**4M页面**的路径: 在**PDT里的entry结构**(PDE)的**PS标志为1(！！！**)时, PDE指向4M的物理page frame(**只需要这一个页表结构！！！**)。线性地址中的[21: 0]域是**22位的4M页面**的**offset**值。

**4KB页面线性地址构成: 10(PDT索引, 1024个项) \+ 10(PT索引, 1024个项) \+ 12(Page offset, 4KB页**)

**黑色线路**是4K页面的路径: 当**PDE.PS=0**时, PDE指向Page table结构, 再由PTE指向最终的4K物理page frame, 线性地址中的[11: 0]域是**12位**的**4K页面的offset**值。

## 1.1. 两种页表结构

从上图我们看到在32位paging模式里使用了**两种页表**。

① **Page Directory Table(页目录表**): 和其中的**PDE结构作为一类**, 线性地址的[31: 22]域**10位**是**PDT的索引值**, 在**PDT**内查找**对应的PDE**结构。

② **Page Table(页表**): 和其中的PTE结构作为一类, **线性地址的[21: 12]域**是PT的索引值, 在PT内查找对应的PTE结构。

## 1.2. page offset值

对于**4K页面**来说**线性地址的[11: 0**]域是page内的**offset**值, 对于**4M页面**来说**线性地址的[21: 0]域**是page内的**offset**值。

## 1.3. Table Entry(表项)的大小

每个**Table Entry结构**为**4个字节32位宽(！！！**), 可是**CR3依然只能使用低32位**。

## 1.4. 页表的大小

在**32位paging模式**下, **PDT**的**索引值**和**PT**的**索引值**是**10位**, 可以查找到**1024个表项(！！！**), 而**每个表项**(PDE和PTE)的大小是**4字节宽(！！！**), 因此**每个PDT和PT的size都是4KB(一个整个页目录表或一个页表！！！**)。

在**完整的一级paging**里总共需要多少个页转换表结构和多少内存量？

这个要视映射的范围来定, 假如要映射**完整的4G虚拟地址**, 以**4K页面映射**的话将需要: 

① 需要**1个PDT**, 占用**4K内存**。

② **PDT**里有**1024个PDE表项**, 那么需要**1024个PT**, 占用**4K×1024=4M内存**。

总共需**1025个页转换表结构**, 占用**4M+4K的内存量**。

如果全部page以**4M页面映射**, 将大大减少内存需求量, **只需要1个PDT**, 占用**4K的内存量**。

# 2. CR3结构

在**32位paging模式**下的**CR3结构**如下。

![config](./images/10.png)

在32位paging模式下, CR3使用**低32位**(在**Intel64或AMD64机器上64位**), CR3的**Bit 31到Bit 12位**提供**20位的Page Directory Table的物理基地址(物理基地址！！！高20位！！！**)。

那么以**36位的物理基地址**为例, 它是这样形成的。

① base of PDT[35: 32]=0值(**高4位Bit 35到Bit 32为0值！！！如果是40位物理地址高位也会是补0！！！**)。

② base of PDT[31: 12]=CR3[31: 12](由**CR3的Page Driectory base域**提供)。

③ base of PDT[11: 00]=0值(**低12位补0！！！**)。

因此CR3提供的**PDT物理基地址(！！！**)是**4K边界对齐(！！！**)的。**和4KB分页的物理地址形成方式一致(！！！**).

32位paging下**最高为40位物理地址(！！！**)(依赖于PSE\-36与MAXPHYADDR值)。

## 2.1. PWT

这是**page级**的**Write\-Through标志位**, 当PWT=1时使用**Write\-Through的Cache类型**, 当PWT=0时使用**Write\-Back**的Cache类型。它受到**CR0.CD控制位**的影响, 当**CR0.CD=1**时(**Cache被Disable掉**)PWT标志**会被忽略**。

## 2.2. PCD

这是page级的**Cache Disable标志位**。当PCD=1时, **物理页面是不能被Cache的**, 清0时允许。当C**R0.CD=1**时, PCD标志位被忽略。

**PWT和PCD标志使用在CR3**中, **整个页转换表结构**都受到**影响(页表结构也是物理页！！！**)。

# 3. 32位paging模式下的PDE结构

如下图所示, 它是**4K页面**和**4M页面**共同使用的结构。

![config](./images/11.png)

当**PDE.PS=1**时, 它将作为**4M页面**的PDE结构, 当PDE.PS=0时, 它作为4K页面的PDE结构, 这两种解析存在区别。

## 3.1. 4K页面的PDE结构

在PDE.PS为0时, 使用4K页面的PDE结构, 如下图所示。

![config](./images/12.png)

在这个结构里, `PDE[31: 12]`域共**20位是Page table结构**的**物理基地址**, 在这个32位paging模式的4K页面里, 能映射的**物理地址最高是32位(！！！4K页最高32位！！！**), 下一级**32位的page table物理地址(只是32位物理地址组成关系！！！**)形成如下。

① base of `PT[31: 12]`(**高20位**)=`PDE[31: 12]`, 它是**PDE的高20位值**。

② base of `PT[11: 0]`(**低12位**)=0值, 32位地址的**低12位补0**。

因此, PDE里提供的**PT物理基地址(！！！**)是**4K边界对齐(！！！由PT物理基地址组成决定的！！！**)的。

### 3.1.1. P(Present)标志位

**必须为1**, 表示**page table或page**是存在内存中, 否则**产生\#PF异常**。它与**段描述符中的P标志位**意义相同, 不同的是**段描述符中的P=0**时**产生\#NP异常**。

### 3.1.2. R/W(Read/Write)标志位

这个标志位指示和决定**物理page的读/写权限**。为**0时是只读**的, 为**1时是可读/写的**。当CR0.WP为0时, Supervisor可以对只读页进行写操作。关于R/W标志位如何决定物理page读/写权限, 在第11.8节有详细描述。

### 3.1.3. U/S(User/Supervisor)标志位

这个标志位指示和决定物理page的访问权限, 当为**0时只有Supervisor(0级、1级或者2级权限**)才能访问, 当为1时User(3级权限)和Supervisor可以访问。关于U/S标志位如何决定物理page的访问权限, 在第11.8节有详细描述。

### 3.1.4. PWT(Page-level Write-Through)标志位

定义页级的Cache类型, 见11.3.1节。

### 3.1.5. PCD(Page-level Cache Disable)标志位

指示页级是否可Cache, 见11.3.1节。

### 3.1.6. A(Accessed)标志位

为1时, 指示和决定**物理page是否已被访问(读或写**)。一旦置位, **处理器从不清这个标志位**。这个标志位**可被OS的内存管理软件利用**。

### 3.1.7. PS(Page Size)标志位

PS标志位仅使用在PDE和PDPE结构中, 为1时, 表示PDE或PDPE指向物理page frame, 为0时, 指向下一级页转换表结构。

在这个4K页面的PDE结构里, PS值为0, 它将指向PT的物理基地址。在PDE结构中灰色部分为忽略域, 不影响使用。

## 3.2. 4M页面的PDE结构

当**PDE.PS为1**时, 使用下面的**4M页面PDE结构**。

![config](./images/13.png)

这个PDE结构里将提供4M页面的基地址, 然而这个4M物理page frame地址的设置, 需要注意。

① 当处理器**不支持PSE\-36**功能时, 4M page frame只能映射到**32位的物理地址空间(！！！**)上, `PDE\[31: 22\]`是`4M page base\[31: 22\]`(**高10位**), 这时候`PDE[21: 13]`共9位是保留位, 必须设置为0值。

② 当**MAXPHYADDR**值为**36**位时, `PDE[16: 13\]`是**4M page base[35: 32]位(高4位**), `PDE[31: 22\]`是`4M page base\[31: 22\]`(**中间10位**), 而`PDE\[21: 17\]`是保留位, 必须设置为0值。

③ 当MAXPHYADDR值为**40位**时, `PDE[20: 13]`是`4M page base\[39: 32\]`位(高8位), `PDE\[31: 22\]`是`4M page base\[31: 22\]`(**中间10位**), 而`PDE[21]`位是保留位, 必须设置为0值。

④ 当MAXPHYADDR值为**52位**时, 也**仅能使用40位的物理地址(！！！**), 同③。

因此, 4M page frame的基地址形成除上述所说外, **36位或40位的物理地址低22位将补0(！！！**), 物理页面将在**4M边界上对齐(！！！**)。

### 3.2.1. D(Dirty)标志位

这个标志位仅被用于描述**page的属性**。当**page被写**时, 处理器将D标志置位, 指示已被写过, **处理器从不会清D标志位**。典型地**D标志和A标志可被OS内存管理软件使用**。

### 3.2.2. G(Global page)标志位

这个标志位仅被用于描述page的属性。当**CR4.PGE=1(！！！**)时, 可置**G标志位为1(！！！**), 指示**page是global page(！！！**)。在**CR3被更新**时, TLB内的**global page不被刷新**。

### 3.2.3. PAT(Page-Attribute Table)标志位

当`CPUID.01H: EDX[16]` support位为1时, 可以使用PAT功能, PAT标志位可被置位。否则为保留位, 必须为0值。它指示了page的属性。

注意, **D标志、G标志以及PAT标志位仅被使用在对page**的管理上。

① 当**4K页面**时, 使用在**PTE结构**上。

② 当**4M和2M页面**时, 使用在**PDE结构**上。

③ 当**1G页面**时, 使用在**PDPE结构**上。

否则在其他page entry页项结构里D标志和G标志被忽略, PAT标志位为0值。

### 3.2.4. 保留位与忽略位

在各级页表项(entry)结构里, 会有保留位或者忽略位。它们的意义是不同的, 保留位必须为0, 处理器会对保留位进行检查。忽略位不会被检查, 不起作用。

### 3.2.5. PS(Page Size)标志位

在**4M和2M页面**里, **PDE(！！！)的PS标志位为1**。在**1G页面**里, **PDPE(！！！IA\-32e的1GB页面就两级页表PML4T和PDPT)的PS标志位为1**。在其他的page**表项结构**里为**0值**(在PTE结构里PS位被用做PAT标志位)。

换句话说, **只有4KB页面的所有表结构PS标志位为0, 其余大小页面机制有PDT的则是在PDT的表项(即PDE)中PS标志位置位1, 没有PDT的则是在PDPT的表项(即PDPE**)

## 3.3. 4K页面的PTE结构

PTE结构**指向物理page frame基址**(在**4M页面里由PDE结构指向！！！**)。4K页的page frame是**4K边界对齐(由物理地址组成决定的！！！**)的。

![config](./images/14.png)

在32位paging模式下**4K页面**使用的是**32位的最高映射物理地址(！！！只能使用最高32位物理地址！！！**)。因此page frame的形成如下。

① `page base[31: 12]`=`PTE[31: 12]`, PTE的**高20位**对应**物理页面的高20位**。

② `page base[12: 0]`=0值。**物理页面的低12位补0**。

所以**物理页面在4KB边界对齐**.

在上图中, PTE结构的Bit7位由PS标志位变为PAT标志位。

### 3.3.1. 描述page的属性

如同4M页面的PDE结构, D标志、G标志, 以及PAT标志被使用在4K页面的PTE结构里。其意义是完全一致的。

# 4. 使用32位paging

到目前为止, protected.asm模块是未开启paging机制的。在这一小节里, 我们将在protected.asm模块里打开paging机制, 并作为一个练习实验来使用32位paging。

>实验11-2: 使用与测试32位paging

这一小节的源代码在topic11\ex11-2\目录下。

## 4.1. 配置页转换表环境

在**开启paging机制前**必须先**设置好各级的页转换表结构**, 代码如下。

代码清单11-3(topic11\ex11-02\protected.asm): 

```x86asm
; ---------------------------------------------
;  init_32bit_paging(): 建立 32位paging 环境
; ---------------------------------------------
init_32bit_paging: 
;  ① 0x000000-0x3fffff 映射到 0x0 page frame, 使用 4M 页面
;  ② 0x400000-0x400fff 映射到 0x400000 page frame, 使用 4K 页面
; ;  PDT 物理地址设在 0x200000 位置上, PT物理地址在 0x201000位置上
;  ① 设置 PDT[0](映射 0 page frame)
      ;  base=0, PS=1, P=1, R/W=1, U/S=1
      mov DWORD [PDT32_BASE + 0], 0000h | PS | RW | US | P
;  ② 设置 PDT[1]
      ;  PT的地址在0x201000位置上, 设置为supervisor, only-read 权限
      ;  PT base=201000h, P=1
      mov DWORD [PDT32_BASE + 1 * 4], 201000h | P
;  ③ 设置 PT[0](映射0x400000 page frame), 设置为supervisor, only-read 权限
      mov DWORD [201000h + 0], 400000h | P  ;  4K page framg=400000h
      ret
```

init\_32bit\_paging()函数的职责是设置32位paging模式下的页转换表结构, 作为练习实验, 为了简明这里只映射了两块区域。

① virtual address的0到0x3FFFFF区域, 使用4M页面来映射。

② virtual address的0x400000到0x400FFF区域, 使用4K页面来映射。

PDT的物理基地址设在0x200000位置, 而PT的物理基地址设在0x201000位置上, 于是virtual address区域0到0x3FFFFF将使用PDE[0], 而0x400000将使用PDE[1], 如上面代码所示, 直接对PDE结构进行设置。

## 4.2. 开启PSE功能

在32位paging分页模式下, 为了使用4M页面, 需要开启PSE功能。

代码清单11-4(topic11\ex11-02\protected.asm): 

```x86asm
pse_enable: 
      mov eax, 1
      cpuid
      bt edx, 3                                ;  PSE support？
      jnc pse_enable_done
      mov eax, cr4
      bts eax, 4                               ;  CR4.PSE=1
      mov cr4, eax
pse_enable_done: 
      ret
```

上面先检测是否支持PSE功能, 通过CPUID.01H: EDX[3].PSE位查询。(检查是否支持PSE似乎有些多余！并且上面的代码也没有在不支持的情况下进行提示。)最后对CR4.PSE进行置位。

当CR4.PSE=0时, 将使用4K页面(PDE.PS=1时作用被忽略)。

因此, 下面两种情况下效果是一致的。

① CR4.PSE=0时: 关闭4M页面。

② CR4.PSE=1且PDE.PS=0时: 使用4K页面。

## 4.3. 开启paging

在对CR0.PG置位时, 必须先设置PDT的物理基地址, 即对CR3赋值。

代码清单11-5(topic11\ex11-02\protected.asm): 

```x86asm
;  初始化 paging 环境
      call init_32bit_paging
; 设置 PDT 地址
      mov eax, PDT32_BASE
      mov cr3, eax
; 设置 CR4.PSE
      call pse_enable
;  打开 paging
      mov eax, cr0
      bts eax, 31
      mov cr0, eax
```

上面的代码是在protected模式下进行设置, 当置CR0.PG为1时, 处理器将立即进入paging机制。在mov cr0, eax指令的下一条指令将使用paging管理, 处理器将在各级page表结构里walk查找EIP指针地址。

最后, 为了直接显示paging机制的效果, 在lib\page32.asm模块里写了一个dump_page()函数, 用来打印出被映射virtual address的PDE和PTE信息, 源代码在page32.asm文件里。

代码清单11-6(topic11\ex11-02\protected.asm): 

```x86asm
mov esi, 0x200000                ;  dump virtual address 0x200000
call dump_page
mov esi, msg3
call puts
mov esi, 0x400000                ;  dump virtual address 0x400000
call dump_page
```
这段代码在用户代码里做测试, 分别打印virtual address 0x200000和0x400000的各级page表结构信息。

下面是实验的结果。

![config](./images/15.png)

结果所示, virutal address 0x200000是在4M页面内, 它的page frame地址是0, 后面显示为Attribute属性, 在Attribute显示里大写为置位, 小写为清0。4M页面的属性为PS=1, User/Writable(用户权限可访问, 可读/写)。

virtual address 0x400000是4K页面, 注意它的D和A标志为0, 显示未曾被访问过。并且它的访问权限是Supervisor和Only-read。它对用户代码有两个方面的限制。

① 只能被Supervisor访问, 拥有0、1和2级权限。

② Only-read限制, 对于用户代码来说是不可写的。

## 4.4. 测试一: 在用户代码里写0x400000地址

下面我们在用户代码里向0x400000地址写数据, 将产生#PF异常。

![config](./images/16.png)

## 4.5. 测试二: 在0级代码里写0x400000地址

对于Supervisor访问者, 当CR0.WP=0时, 它可以写Only-read页面。下面我们测试在0级代码里写0x400000地址, 结果如下。

![config](./images/17.png)

注意, 此时0x400000页面的D标志和A标志已经被置位(已被写访问过)。

## 4.6. 测试三: 在开启WP功能下, 0级代码里写0x400000地址

代码清单11-7(topic11\ex11-02\protected.asm): 

```x86asm
; 测试二: 在 CR0.WP=0时, 在0级代码里写0x400000地址
mov DWORD [0x400000], 0
; 测试三: CR0.WP=1时, 写0x400000
mov eax, cr0
bts eax, WP_BIT
mov cr0, eax
mov DWORD [0x400000], 0
```

在0级代码里开启CR0.WP控制位后, 再尝试往0x400000地址里写数据, 那么将产生#GP异常。

![config](./images/18.png)

结果所示, 执行流程未进入用户代码就产生了#PF异常。