
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. APIC概述](#1-apic概述)
- [2. I/O APIC寄存器](#2-io-apic寄存器)
  - [2.1. 访问I/O APIC寄存器](#21-访问io-apic寄存器)
  - [2.2. direct register(直接寄存器)](#22-direct-register直接寄存器)
  - [2.3. I/O APIC寄存器的基址](#23-io-apic寄存器的基址)
  - [2.4. 开启和设置I/O APIC基址](#24-开启和设置io-apic基址)
- [3. indirect register(间接寄存器)](#3-indirect-register间接寄存器)
  - [3.1. I/O APIC ID寄存器](#31-io-apic-id寄存器)
  - [3.2. I/O APIC version寄存器](#32-io-apic-version寄存器)
  - [3.3. Redirection table寄存器](#33-redirection-table寄存器)
- [4. I/O APIC的IRQ](#4-io-apic的irq)
- [5. I/O APIC的中断处理](#5-io-apic的中断处理)
  - [5.1. I/O APIC与8259并存时的处理](#51-io-apic与8259并存时的处理)

<!-- /code_chunk_output -->

# 1. APIC概述

APIC分为两部分: **local APIC**与**I/O APIC**。local APIC位于**处理器内部**, 而I/O APIC则**属于芯片组的一部分**。local APIC与I/O APIC通过system bus进行通信。

![config](./images/1.png)

上图展示了**I/O APIC**是如何与处理器通信的, 像处理器间通信一样, I/O APIC发送**中断消息**从**PCI桥**通过**system bus**到达**指定的处理器(或一组处理器**)。

**I/O APIC**与**8259兼容类的中断控制器**最大的不同是, **8259中断控制器**的输出口INTR连接到**处理器的INTR引脚**(当**local APIC**开启时, 连接到**local APIC的LINT0口**上)。在8259向处理器发送中断请求过程中, 8259与处理器需要**来回的通信响应**。详情请参考17.1.5节所述。

而**I/O APIC**通过**直接写**I/O APIC设备**内存映射的地址空间(！！！**), 在system bus中**传送中断消息**, **无须处理器发出acknowledge cycle(响应周期)确认**。因此, I/O APIC的中断响应更快。

# 2. I/O APIC寄存器

I/O APIC的寄存器也是通过**内存映射方式**映射到处理器**物理地址空间**。

**I/O APIC的寄存器**工作在**两种访问方式**: 

- **direct register(直接访问的寄存器**)
- **indirect register(间接访问的寄存器**)。

## 2.1. 访问I/O APIC寄存器

通过向**index寄存器(直接访问寄存器**)写入需要访问的**I/O APIC寄存器(间接访问寄存器**)的**index值**, 然后从**data寄存器**读出I/O APIC寄存器的值。

```x86asm
mov DWORD [IOAPIC_INDEX_REG], 10h    ;向index寄存器写入低32位index值
mov eax, [IOAPIC_DATA_REG]           ;从data寄存器读10h寄存器的低 32 位
mov DWORD [IOAPIC_INDEX_REG], 11h    ;写入高32位index值
mov edx, [IOAPIC_DATA_REG]           ;读10h寄存器的高32位
```

在上面的代码示例里, 在**32位**代码下: 当**读的寄存器是64位宽**, 那么需要分**两次读(每次只能读32位寄存器**)。寄存器的index分两次写入index寄存器, 再从data寄存器读出值。

```x86asm
mov DWORD [IOAPIC_INDEX_REG], 10h    ;向index寄存器写入index值
mov rax, [IOAPIC_DATA_REG]           ;读入完整的64位值
```

在**64位**代码下, 软件可以**一次读取64位I/O APIC寄存器**的值, 寄存器的**index**值必须是**8 byte边界**上。

## 2.2. direct register(直接寄存器)

直接访问的寄存器有三个, 如下表所示。

![config](./images/2.png)

**软件**通过**index和data寄存器**来**间接访问I/O APIC的所有寄存器**。EOI(End Of Interrupt)寄存器**仅使用在level触发模式(！！！**)上, 对**edge触发模式的中断无影响**。

## 2.3. I/O APIC寄存器的基址

在上表中的直接访问寄存器里, 地址中xx(bit 19到bit 12)代表它们的地址是未确定的, 最终由**OIC(other interrupt controller)控制器来决定**。

![config](./images/3.png)

**OIC寄存器**的**bit 7～bit 0**指定了**I/O APIC寄存器的基地址(！！！**), 对应I/O APIC direct寄存器的**xx值**(bit 19～bit 12)。在默认情况下, **OIC寄存器的值为0h**, 那么**I/O APIC寄存器的地址值就是: index寄存器为FEC00000h, data寄存器为FEC00010h, EOI寄存器为FEC00040h**。

**BIOS可能会选择修改这个值**, 软件需要查询或者直接设定一个值来保证正确访问I/O APIC寄存器。

**OIC寄存器的地址**位于**RCBA(root complex base address)寄存器(PCIE的RC基地址！！！IO APIC属于PCIE设备！！！**)的**31FEh偏移量**上。需要访问OIC寄存器必须**先得到RCBA寄存器的地址**。

代码清单19-1(lib\pic.asm): 

```x86asm
；-------------------------------------------------
； get_root_complex_base_address(): 得到 RCBA 地址
； output: 
；       eax - RCBA address(memroy space)
；-------------------------------------------------
get_root_complex_base_address: 
; 读 RCBA 寄存器
       READ_PCI_DWORD  0, 31, 0, 0F0h   ;读 bus 0, device 31, function 0, offset F0h
       and eax, 0FFFFC000h              ;得到 base address
       ret
```

上面的get\_root\_complex\_base\_address()函数用来获得RCBA寄存器地址, **RCBA寄存器**位于**PCI bus上的device 31设备(LPC bridge**),通过**宏READ\_PCI\_DWORD(！！！用来读取PCI信息,传BDF信息即可**)读取。
```x86asm
READ_PCI_DWORD  0, 31, 0, 0F0h
```

上面的代码使用**READ\_PCI\_DWORD宏**来**读取bus 0, device 31, function 0第F0h偏移量的寄存器！！！**, 这个寄存器就是**RCBA寄存器！！！**, 其中bit 31～bit 14就是RCBA基地址(物理地址)。这个宏实现在inc\pci.inc头文件里。

**重要(！！！**): 通过**PCI的宏**找到**RCBA基地址**, 然后通过**相应的偏移**找到**OIC寄存器(IO APIC被认为是一个controller！！！)**, 通过该**寄存器的相应位获取I/O APIC寄存器的基址**, IO APIC的**相关寄存器**都是在该**基址的相应偏移位置**.

## 2.4. 开启和设置I/O APIC基址

**OIC寄存器**的另一个重要作用是**开启I/O APIC**。bit 8置位时开启I/O APIC, I/O APIC地址可用, 下面的代码来做这项工作。

代码清单19-2(lib\ioapic.asm): 

```x86asm
;------------------------------------
; enable_ioapic(): 开启 ioapic
;------------------------------------
enable_ioapic: 
       ;开启 ioapic
       call get_root_complex_base_address
       mov esi, [eax + 31FEh]
       bts esi, 8                       ;IOAPIC enable 位
       and esi, 0FFFFFF00h              ;IOAPIC range select
       mov [eax + 31FEh], esi           ;enable ioapic
       ;设置 IOAPIC ID
       mov DWORD [IOAPIC_INDEX_REG], IOAPIC_ID_INDEX
       mov DWORD [IOAPIC_DATA_REG], 0F000000h          ;IOAPIC ID=0Fh
       ret
```

得到OIC的值(RCBA+31FEh)后, 第8位置位, 并且将I/O APIC range select值设为0。那么我们的I/O APIC将使用默认的地址值。

代码清单19-3(inc\ioapic.inc): 

```
%define IOAPIC_INDEX_REG                    0FEC00000h
%define IOAPIC_DATA_REG                     0FEC00010h
%define IOAPIC_EOI_REG                      0FEC00040h
```

在inc\ioapic.inc文件里定义了这些寄存器常量值, 方便在程序代码里进行访问。注意, 在笔者的代码里这些地址值使用的paging映射方式是: 虚拟地址和物理地址相同。

当然, 完全可以使用不同的映射方式(只是需要注意, 得到RCBA寄存器时返回的是物理地址, 但是你并不知道RCBA值是多少, 会造成映射上的一些困扰, 最好是在开启paging机制前开启I/O APIC和设置I/O APIC基地址值)。

# 3. indirect register(间接寄存器)

现在**PCH(platform controller hub**)芯片上的**I/O APIC**只有**3组间接访问寄存器**: 

- **I/O APIC ID寄存器**, 
- **I/O APIC version寄存器**
- **24个interrupt redirection table寄存器**。

如下表所示。

![config](./images/4.png)

**每个redirection table寄存器**是**64位**宽, 在32位环境下访问需要分高/低32位进行两次读或写操作。在64位代码下可以一次性访问64位寄存器。

## 3.1. I/O APIC ID寄存器

这个寄存器如同local APIC ID寄存器一样重要, 当有**多个I/O APIC芯片**存在时, 唯一指定I/O APIC的编号。**软件必须为I/O APIC设置一个ID值(！！！软件设置！！！**)。

![config](./images/5.png)

如上所示, I/O APIC ID寄存器的bit 27～bit 24共**4位(同时只能支持16个IO APIC？？**)值为ID值, **软件在使用前必须设置一个值**, I/O APIC ID值将会在**system bus**的**仲裁上使用**到。在前面代码清单19-2里的enable\_ioapic()函数里将I/O APIC ID值设为0Fh。

Bit 15位是scratchpad位, 笔者不知道这个位的作用, Intel的PCH文档里并没有给出说明。

## 3.2. I/O APIC version寄存器

**每个I/O APIC芯片**的**version寄存器**指明了I/O APIC在**implementation(硬件实现**)上的情况, 包括了**I/O APIC的版本**和**最大包含的redirection table数量**。

![config](./images/6.png)

如上所示, version寄存器的bit 23～bit 16域指明了redirection table寄存器的数量。PCH硬件设这个值为17h(即24), 表示有24个redirection table寄存器, 而version的值为20h。

## 3.3. Redirection table寄存器

> 中断重定向表

可以这样认为, **Redirection table寄存器**的作用结合了local APIC的**LVT(local vector table)寄存器**和**ICR(interrupt command register**)的功能。既像LVT寄存器一样**产生中断消息**, 也像ICR一样具有**发送中断消息**到目标处理器的功能。

而这个**中断消息**是当**IRQ线**上的**外部硬件产生中断请求**时, **I/O APIC**从**redirection table寄存器**读取中断信息**发送到目标处理器**。

![config](./images/7.png)

从整个redirection table角度看, **每个redirection table寄存器**就是一个**RTE(redirection table entry**)。在上图的redirection table寄存器结构与**LVT寄存器**、**ICR大致相同**, I/O APIC中断消息也需要提供下面的四大要素。

① 中断服务例程的**vector值**。

② 中断的**Delivery mode**: 支持Fixed、lowest priority、SMI、NMI、INIT, 以及ExtINT模式。

③ **触发模式**: edge或level触发模式。

④ **目标处理器**: 提供**physical**或**logical目标模式**, 在**destination域**里提供**目标处理器ID值**。

中断服务例程的有效vector在16～255(软件应避免使用16～31保护模式下系统使用的异常处理vector)。对于**Fixed**与**lowest priority交付模式**需要**显式提供vector**, 而**SMI、NMI及INIT交付模式**vector域必须**写0**。

**ExtINT交付模式(！！！**)被使用在**I/O APIC**连接着**外部的8259兼容类中断控制器**, 由**8259中断控制器**提供**vector值**。

# 4. I/O APIC的IRQ

I/O APIC的**redirection table**表支持**24个RTE**(即**24个redirection table寄存器**), 每一个redirection table寄存器**对应一条IRQ(interrupt request)线**, 因此**I/O APIC支持24条IRQ线(！！！**),  **每根线**可以连接**一个外设！！！**。

如下表所示。

![config](./images/8.png)

在I/O APIC的**IRQ0线**上连接着**8259中断控制器的INTR输出口**, **redirection table 0寄存器**需要**使用ExtINT delivery模式**, 由**8259中断控制器提供vector**, 如下面的代码所示。

```x86asm
mov DWORD [IOAPIC_INDEX_REG], IRQ0_INDEX    ;RTE0低32位index值
mov DWORD [IOAPIC_DATA_REG], PHYSICAL_ID | EXTINT   ;使用 ExtINT delivery模式
mov DWORD [IOAPIC_INDEX_REG], IRQ0_INDEX + 1    ;RTE0高32位index
mov DWORD [IOAPIC_DATA_REG], 0      ;APIC ID=0
```

在PCH中, **HPET(high precision event timer, 高精度定时器**)的**timer 0**可以连接到**I/O APIC**的**IRQ2线**, 而在**8259中断控制器**里**IRQ2线**串接着**8259从片**。除了**IRQ0和IRQ2线的不同**外, **I/O APIC**与**8259**的**IRQ1、IRQ3～IRQ15连接的硬件设备和作用是相同**的。

**I/O APIC**比8259多出了**8条IRQ线**, 可以由PCH上的**每个PCI设备**的**4个interrupt pin连接(！！！**)。在**PCH中所支持**的**8个PCI设备**及它们**所使用的IRQ线**如下表所示。

![config](./images/9.png)

这些设备都是在**PCI bus 0(！！！**)上, **8个设备**的**4个中断口**(INTA\#～INTD\#)可以**配置route(转发**)到redirection table上的**8个IRQ中断源**(PIRQA\#～PIRQH\#), 由**PCH**的**chipset configure register**(即**RCBA寄存器**, 由前面所说的get\_root\_complex\_base\_address()函数来获得)中**各自**的**device interrupt route register(设备中断通路寄存器**)进行设置。

因此, I/O APIC的**IRQ16～IRQ23(8个IRQ, 即对应8个RTE！！！**)对应着上面的**8个PCI设备中断口(！！！**)。

# 5. I/O APIC的中断处理

I/O APIC对于8259的优势是明显的, 当有**多个I/O APIC芯片**存在时, 每个I/O APIC有它自己的**I/O APIC ID标识**, **每个redirection table寄存器**有自己的**vector**。I/O APIC处理外部中断的能力将大幅提升。

I/O APIC的**中断处理与8259有极大的不同**: **8259**向**BSP处理器(！！！所以无法用到MP多处理器平台！！！**)发送的**中断请求消息**需要处理器**以INTA周期来响应**, 而I/O APIC直接发中断消息到处理器, 属于**直接触发形式**, 并且I/O APIC中断消息可发送至system bus上的**指定目标处理器**。

**system bus属于广播总线**, 也就是说**所有的system bus**上的消息**每个CPU都可以看得到**, 也都**可以处理**.

对于中断**请求仲裁**和**优先级**, 也有下面的不同。

① **I/O APIC对中断的仲裁**使用**local APIC(！！！IO APIC本身不仲裁！！！**)的**IRR(interrupt request register)、ISR(in-service register)、TPR(task priority register), 以及PPR(processor priority register**), 受到**local APIC的制约**。而**8259**使用控制器自己**内部的IMR(interrupt mask register)、IRR, 以及ISR仲裁**。

② 8259的中断优先级按**IRQ次序进行排序**, 在初始状态下优先级是**IRQ0、IRQ1、IRQ8～IRQ15、IRQ3～IRQ7的次序**。由于受到local APIC的影响, **I/O APIC**的**优先次序**是按**IRQ的中断vector大小**来排序。

IOAPIC通常有**24个不具有优先级的管脚**, 用于**连接外部设备**. 当**IRQ线(连接的外设线！！！**)上有中断请求发生时, **I/O APIC**在**redirection table(通常是操作系统设定的**)里找到管脚对应的**RTE(redirection table entry或者说redirection table寄存器**), 读取**RTE**内的**中断消息内容**, 通过其各个字段, 格式化出一条包含该中断所有信息的中断消息, 再经由**system bus(！！！老式的通过专门的APIC总线**), 那么后续的中断处理器流程就和18.11节所述的“local APIC的中断处理”是一致的。

中断消息发送到system bus后, 就已经**不属于I/O APIC的处理范围**了, 而是在system bus硬件和local APIC的管辖之内。最终的中断消息将由local APIC来接收处理。

![config](./images/10.png)

上图是I/O APIC对**IRQ1线**上的**键盘中断请求**处理的示意, 可以认为**I/O APIC**的职责主要是**读取RTE和转送中断消息**。当**按下一个键I/O APIC检测并确认接受中断**, 在**redirection table寄存器**里的delivery status状态位里记录(**置位时pending状态**), 组织发起一次中断消息到**system bus**, 处理器接收中断消息根据中断的vector在local APIC的IRR(interrupt request register)相应位置位。

在通过中断优先级仲裁后进入ISR(中断服务例程), **redirection table**寄存器的**delivery status恢复idle(空闲**)状态允许接受**下一次键盘中断(！！！**)。

## 5.1. I/O APIC与8259并存时的处理

PCH里是**同时存在8259与I/O APIC芯片**的, **软件上**需要**避免硬件的中断同时由8259和I/O APIC来处理**。当8259和I/O APIC的功能重叠时, 例如: **8259和I/O APIC**可以同时接收**键盘中断请求**时, 将会进行两次中断处理。

① 一次是**BSP处理器响应8259提交的中断请求**。

② 一次是**目标处理器响应I/O APIC提交的中断请求**。

比较有趣的是, 当I/O APIC发送的中断消息的目标处理器不是BSP时, 这两个中断服务例程**可能会同时进行**, 如果涉及共同访问同一块内存时, 这样又会造成错乱。

总而言之, 必须避免同时处理的这种情况发生。显然, 由**I/O APIC统一处理所有的中断请求是个好主意**。我们需要**屏蔽所有来自8259的中断请求**。

下面的代码从**8259中断控制器**里进行屏蔽。

```x86asm
mov al, 0FFh      ;所有 mask 位置位
out 21h, al       ;写入 8259 master片的 IMR
```

下面的代码**从BSP的local APIC里进行屏蔽**。

```x86asm
bts DWORD [APIC_BASE + LVT_LINT0], 16   ； LINT0 的mask位置位
```

关于**屏蔽LINT0寄存器**的方法介绍, 详情请参考18.14.2节的描述。注意, 必须对BSP处理器的LVT LINT0进行屏蔽, 因为8259只能向BSP发送中断请求。

>实验19-1: 使用I/O APIC处理键盘中断

在这个实验里, 我们将输入一些按键并回显出来, 测试I/O APIC的中断处理方式。实验的IRQ1中断服务例程实现在lib\ioapic.asm文件里。

代码清单19-4(lib\ioapic.asm): 

```x86asm
；-----------------------------------
； ioapic_keyboard_handler()
；-----------------------------------
ioapic_keyboard_handler: 
       in al, I8408_DATA_PORT                   ； 读键盘扫描码
       movzx eax, al
       cmp eax, key_map_end - key_map
       jg ioapic_keyboard_handler_done
       mov esi, [key_map + eax]
       call putc        ； 打印按键
ioapic_keyboard_handler_done: 
       mov DWORD [APIC_BASE + EOI], 0           ； 发送 EOI 命令
       iret
```

这个ioapic\_keyboard\_handler()很简单, 读取键盘扫描码后转换为ASCII字符打印在屏幕上。作为使用I/O APIC的演示例子, 并没有进行什么按键的处理。最后别忘了需要加上给local APIC发送EOI命令的指令。

注意, 从18.5节所述, 当local APIC的SVR(Spurious-Interrupt Vector Register, 伪中断向量寄存器)的bit12为0时, 对于I/O APIC的Fixed交付模式并且是level触发的中断, 给local APIC发送EOI命令, local APIC也会广播EOI给I/O APIC。

代码清单19-5(topic19\ex19-1\protected.asm): 

```x86asm
；实验 ex19-1: 使用I/O APIC处理键盘中断
       cli
       ；* 建立 IRQ1 中断服务例程
       mov esi, IOAPIC_IRQ1_VECTOR
       mov edi, ioapic_keyboard_handler
       call set_interrupt_handler
       ；* 设置 IOAPIC 的 redirection table 1 寄存器
       mov DWORD [IOAPIC_INDEX_REG], IRQ1_INDEX
       mov DWORD [IOAPIC_DATA_REG],  LOGICAL | IOAPIC_IRQ1_VECTOR
       mov DWORD [IOAPIC_INDEX_REG], IRQ1_INDEX + 1
       mov DWORD [IOAPIC_DATA_REG], 08000000h    ； 目标处理器是 processor #4
       ； 屏蔽 LINT0
       bts DWORD [APIC_BASE + LVT_LINT0], 16
       ； 打印等待信息
       mov esi, msg
       call puts
       sti
       jmp $
```

上面是实验的主体代码, 它所做的工作主要如下。

① 建立IRQ1中断服务例程对应的IDT表项(使用set\_interrupt\_handler()函数)。

② 设置I/O APIC的IRQ1所对应的redirection table寄存器信息, 使用的是logical目标模式, 而目标处理器将使用processor #4(第4个逻辑处理器)。

③ 屏蔽8259的所有中断请求。

在设置前最好先关闭中断许可, 避免设置完成前发生中断。最后, 打印等待信息, 等待用户按下按键。键盘服务例程将会打印出来用户的按键。下面是实验的运行结果。

![config](./images/11.png)

在运行时, 笔者输入了“hello, world”按键后, 如上面的结果回显在屏幕上。笔者最后输入了一个回车键, 会显示一个乱码符号, 这是由于没有对其他按建进行处理。