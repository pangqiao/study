
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [检测处理器是否支持long\-mode](#检测处理器是否支持long-mode)
- [EFER寄存器](#efer寄存器)
  - [SCE（System\-Call Extension）位](#scesystem-call-extension位)
  - [LME（Long Mode Enable）位](#lmelong-mode-enable位)
  - [LMA（Long Mode Active）位](#lmalong-mode-active位)
  - [NXE（No-Exectue Enable）位](#nxeno-exectue-enable位)
  - [SVME（Secure Virtual Machine Enable）位](#svmesecure-virtual-machine-enable位)
  - [LMSLE（Long Mode Segment Limit Enable）位](#lmslelong-mode-segment-limit-enable位)
  - [FFXSR（Fast FXSAVE/FXRSTOR）位](#ffxsrfast-fxsavefxrstor位)
- [进入long-mode的必要条件](#进入long-mode的必要条件)
  - [long-mode的enable与active](#long-mode的enable与active)
  - [进入long-mode的制约条件](#进入long-mode的制约条件)

<!-- /code_chunk_output -->

在支持**AMD64**或**Intel64架构**的处理器上，可以选择**不同的进入long\-mode途径**，如下所示。

![config](./images/5.png)

从**实模式**中**先切换到保护模式**，再切换到long\-mode。也可以直接从实模式中切换到long\-mode。无论怎样进入long\-mode都应该重新设置long\-mode的执行环境。

# 检测处理器是否支持long\-mode

能进入long\-mode的首要条件是**处理器必须支持long\-mode**，要检测是否支持long\-mode，我们使用下面的示例代码。

代码清单12-1（lib\cpuid.asm）：

```x86asm
；---------------------------------------------------
； support_long_mode（）：检测是否支持long-mode
； output：
；  1-support，0-no support
；---------------------------------------------------
support_long_mode：
      mov eax，80000000H
      cpuid
      cmp eax，80000001H    ； 检测是否支持 80000001H leaf
      setnb al
      jb support_long_mode
      mov eax，80000001H
      cpuid
      bt edx，29       ； long mode support 位
      setc al
support_long_mode_done：
      movzx eax，al
      ret
```

在这个检测中，我们必须先检测处理器是否支持**CPUID的扩展leaf**（使用80000000H leaf来检测所支持的最大扩展leaf号），在支持80000001H leaf的情况下检测80000001H leaf的bit 29位（long-mode的support位）是否为1值。

# EFER寄存器

为了支持long\-mode，AMD引入了**EFER（Extended Feature Enable Register）寄存器**，EFER寄存器是**MSR寄存器**（地址在C0000080H），如下所示。

![config](./images/6.png)

AMD的EFER寄存器不仅是为了支持long-mode模式而引进，还负责开启**AMD SVM（Secure Virtual Machine**）技术。

## SCE（System\-Call Extension）位

设置这个位将开启Syscall/Sysret指令，这对指令是AMD用来在long\-mode里替换Sysenter/Sysexit指令的，它们作用是一样的。

## LME（Long Mode Enable）位

对这个位置位将开启long\-mode，但**long\-mode的active（激活**）还需要依赖于paging的开启。

## LMA（Long Mode Active）位

这个位是**只读位**，它由**处理器自动维护(！！！不能由软件设置！！！**)。当**LME=1时（前提！！！**)，对**CR0.PG进行置位**，将使处理器**对LMA置位**，表明处理器处于**long\-mode**下。

## NXE（No-Exectue Enable）位

置位时，允许软件设置page table entry的NX（bit 63位）标志，否则各table entry的bit 63为保留位。

## SVME（Secure Virtual Machine Enable）位

置位将开启AMD的SVM技术。

## LMSLE（Long Mode Segment Limit Enable）位

由于在long\-mode的64位模式下，处理器不会对各个segment的limit进行检查。在AMD64下，允许置LMSLE位，开启在非0级权限下对data segment（DS、ES、FS，以及SS段）进行段限检查，然而这个段限检查并不包括CS和GS段，以及CPL=0的情况。这个标志位在Intel64中不存在。

## FFXSR（Fast FXSAVE/FXRSTOR）位

在AMD64下，对这个位置位允许在64位模式的CPL=0权限下执行FXSAVE和FXRSTOR指令无须保存XMM0到XMM15寄存器的状态值。

在**Intel64架构**下，EFER寄存器被称为**IA32\_EFER寄存器**，然而与AMD64的EFER寄存器有些区别，如下所示。

![config](./images/7.png)

我们看到，IA32\_EFER寄存器并没有LMSLE位，表明Intel处理器在64位模式下没有开启段限检查的功能，同时也没有快速的FXSVAE/FXRSTOR功能。

# 进入long-mode的必要条件

前面提过，long\-mode实际上是扩展的保护模式。因此，**开启long\-mode必须要开启保护模式**。进入long\-mode**必须要**：

① CR0.PE=1，开启**保护模式**。

② CR4.PAE=1，开启**PAE（Physical Address Extension）模式**。

③ CR0.PG=1，开启**paging机制**。

**Long\-mode必须启用PAE模式的分页机制（形成long\-mode下的分页模式**）。在**Intel64**中，被称为**IA\-32e paging模式**（关于IA\-32e paging模式，请参考11.5节所述）。

## long-mode的enable与active

当**EFER.LME=1**（或**IA32\_EFER.LME=1**）时，处理器**开启long\-mode**，然而在EFER.LMA=0（或IA32\_EFER.LMA=0）时，这个long\-mode是不可用的，**必须开启paging机制后**，long\-mode才被激活（可用）。

![config](./images/8.png)

![config](./images/29.png)

"**进长模式必先进入保护模式**"是因为**PE标志位被置位**, 实际代码编写可以跳过保护模式代码, 直接进入长模式. 

如上所示，从**实模式**里**直接进入long\-mode**，在long\-mode开启但**未active（激活**）时，即在开启分页机制（CR0.PG=1）前，处理器还是处于实模式状态。同样，在从保护模式进入long\-mode，未激活时（开启分页机制前），处理器还是处于保护模式状态。直到CR0.PG=1后，long\-mode被激活，处理器真正处于long\-mode下。

```c
if （IA32_EFER.LMA == 1）
{
      /* 处于 long-mode 下 */
}
else
{
      /* 非 long-mode 下*/
}
```

如上所示，在一个判断是否处于long\-mode的逻辑里，只能通过**EFER.LMA**标志进行判断，从**EFER.LME**标志判断就显得不够严谨。

## 进入long-mode的制约条件

由于long\-mode存在enable与active的情况，这就产生了进入long\-mode的一些制约条件。

① long\-mode被激活，必须开启paging机制（CR0.PG=1）。

② 在开启paging机制前，必须先**开启PAE模式**（CR4.PAE=1）。

③ 在开启paging机制前，必须先开启（或者同时）开启保护模式（CR0.PE=1）。

④ 在开启paging机制（long\-mode激活）前，必须先开启**long\-mode的enable**（IA32\_EFER.LME=1）。

因此，最后一步必须是对CR0.PG置位（或者同时对CR0.PE置位），而**开启PAE模式**必须在**开启分页机制之前**进行，下面是典型的流程。

```c
if （CR0.PG == 1）
{
      CR0.PG=0；  /* 先关闭 paging 模式 */
}
init_page（）；   /* 初始化 long-mode 的页表结构 */
CR3=PML4T_BASE；  /* 加载 CR3 寄存器 */
CR4.PAE=1；   /* 开启 PAE 模式 */
EFER.LME=1；   /* long-mode 的 enable */
if （CR0.PE == 0）
{
      CR0.PE=1；  /* 开启保护模式 */
}
CR0.PG=1；   /* 开启 paging 模式，激活 long-mode */
```

当然，实际上会比上面的流程复杂得多，从保护模式进入long-mode的情况下，如果已经启用paging机制，那么必须先关闭paging机制，这是由于上面所说的制约条件（在CR0.PG=1前，必须先开启EFER.LME=1）。否则将会产生#GP异常。

>实验12-1：直接从实模式转入到long-mode

为了突出，这个实验我们选择直接从实模式切换到long-mode里，切换到long-mode直接在setup.asm模块里进行。

代码清单12-2（topic12\ex12-1\setup.asm）：

```x86asm
cli       ； 关闭中断
      NMI_DISABLE     ； 关闭 NMI
； 测试是否支持 long-mode
      call support_long_mode
      test eax，eax
      jz no_support
； 加载 GDTR
      db 66h        ； 使用 32 位的 operand size
      lgdt [GDT_POINTER]
； 开启 PAE
      mov eax，cr4
      bts eax，5     ； CR4.PAE=1
      mov cr4，eax
； 初始化long-mode 页表结构
      call init_page
； 加载 CR3 寄存器
      mov eax，5000h    ； PML4T 表基址放在 0x5000 位置上
      mov cr3，eax
； 开启long-mode
      mov ecx，IA32_EFER
      rdmsr
      bts eax，8     ； IA32_EFER.LME =1
      wrmsr
； 使用实模式下的中断打印信息
      mov si，msg0
      call puts
； 加载 long-mode的 IDT表
      db 66h
      lidt [IDT_POINTER]
； 开启 PE 和 paging（激活 long-mode）
      mov eax，cr0
      bts eax，0      ； CR0.PE =1
      bts eax，31      ； CR0.PG=1
      mov cr0，eax     ； IA32_EFER.LMA=1
      jmp 28h：entry64     ； 转入 64位 代码
no_support：
      mov si，msg1
      call puts
      jmp $
；；； 以下是 64位 模式代码
    bits 64
entry64：
      mov ax，30h         ； 设置 data segment
      mov ds，ax
      mov es，ax
      mov ss，ax
      mov esp，7FF0h
      mov esi，msg2
      call puts64
      jmp $
```
这个setup.asm模块是运行在**实模式**下，切换到**long\-mode**下，作为一个例子，这里的典型流程如下。

① 检测处理器是否支持long\-mode。

② 加载GDT和IDT。

③ 开启PAE模式。

④ 初始化**long\-mode的页转换表结构**。

⑤ 加载CR3寄存器。

⑥ 开启long\-mode。

⑦ 同时开启保护模式和paging机制，激活long\-mode。

这里为了直观演示效果，在激活long\-mode前，插了一段使用实模式的int 10h中断来打印信息，再加载long\-mode的IDT。在进入64位代码后，再打印一条信息。

下面是在Bochs中的运行结果。

![config](./images/9.png)

注意它的输出，第1次显示处于实模式，第2次显示进入了long\-mode的64位模式里。这个结果正是我们想看到的。

在这个实验里，我们**并没有进入保护模式**，而是选择直接从实模式进入long\-mode。然而这个long\-mode执行环境并不是最终的，当进入long\-mode后，我们应该在long\-mode进行最终的执行环境设置。